---
title: "Implementando Lox en Haskell"
short-title: "Lox en Haskell"
date: 2026-02-01
page:
  bodyHtml: |
    <snippet var="js.mermaid" />
tags:
  - haskell
  - fp
  - tech
  - type-driven development
  - üá™üá∏
---
<!-- LTeX: language=es -->

> [!info] O, en resumen,
> _Qu√© tal estuvo implementar un lenguaje de programaci√≥n orientado a objetos basado en clases
> usando **programaci√≥n funcional pura y est√°ticamente tipada**._
>
> ![Jean-Baptiste Emmanuel Zorg diciendo "mi favorito".](https://camo.githubusercontent.com/c553f4c74ab9dba08d78c9511cfd7a4fd19dd4047ac86e608ea581d3526778be/68747470733a2f2f6d656469612e74656e6f722e636f6d2f584a77773370595a59394d41414141432f7a6f72672d6d792d6661766f75726974652e676966)
>
> Transparencia referencial, inmutabilidad, evaluaci√≥n perezosa, programaci√≥n guiada por tipos, m√≥nadas,
> c√°lculo lambda y otros amigos.

Siempre me atrajo la programaci√≥n funcional propuesta por **Haskell**.

Programar utilizando propiedades matem√°ticas, de forma declarativa y con las dependencias
expl√≠citas, ver c√≥mo encajan todas las piezas (y el compilador acepta el programa) cuando tu modelo
de tipos se ajusta al problema, notar c√≥mo tu entendimiento mejora en el proceso.

Otra historia.

Como tambi√©n me pasa con **Nix**, toda aquella pieza de tecnolog√≠a con estos principios ‚Äîa menudo
tras curvas de aprendizaje popularmente elevadas‚Äî, que prometa a cambio una mejor perspectiva sobre
la programaci√≥n o alguno de sus aspectos tangenciales suele ganar mi inter√©s.

Quiz√° solamente es porque juego al **Bloodborne** y
aplico su filosof√≠a a todos los dem√°s aspectos de mi vida.
En cualquier caso, desde hace a√±os, Haskell es mi lenguaje para proyectos paralelos
y mis entornos de desarrollo y despliegue se describen v√≠a Nix.

Estas perspectivas me acompa√±an hoy d√≠a en mi trabajo diario con Rust, mi lenguaje profesional.
Creo que soy mejor programador gracias a ellas.

## Fabricando int√©rpretes

Algunos miembros del equipo de Agent Control en New Relic tenemos un club de lectura semanal.
Hace unos meses seleccionamos **Crafting Interpreters** de Robert Nystrom (puedes leerlo
gratis en [su web](<https://www.craftinginterpreters.com>)), en gran parte por ser de naturaleza m√°s
pr√°ctica que las lecturas anteriores ([**Rust for Rustaceans**](https://rust-for-rustaceans.com) de
Jon Gjenset y [**Asynchronous Programming in Rust**](https://www.packtpub.com/en-us/product/asynchronous-programming-in-rust-9781805128137) de Carl Fredrik Samson).

El libro implementa un lenguaje de programaci√≥n orientada a objetos simple llamado **Lox**. Lo hace
dos veces, con estrategias y lenguajes diferentes cada vez. Primero en Java (`jlox`) mediante
recorrido de √°rboles, luego en C (`clox`) con una m√°quina virtual de _bytecode_.

Como no quer√≠amos hacer la primera parte en Java (xd) cada uno de nosotros escogi√≥ su propio
lenguaje para la primera parte del libro.

As√≠ que ahora ya sabes a qu√© vino la introducci√≥n.

Este art√≠culo explora varios aspectos de mi implementaci√≥n de `jlox` en Haskell.

> [!tip] ¬°Puedes ver mi implementaci√≥n mientras lees!
> El c√≥digo de mi implementaci√≥n, llamada (no muy originalmente) `hox`, est√° publicado
> [en GitHub](https://github.com/DavSanchez/hox).
> Pasa toda la _suite_ de tests del repositorio oficial de Crafting Interpreters (m√°s sobre esto a
> continuaci√≥n) para `jlox`, por lo que es conforme a lo especificado en el libro.

Vamos all√°.

## Entorno reproducible con Nix

Te lo dije.

Haskell utiliza [`cabal`](https://www.haskell.org/cabal/) como gestor de proyectos. Creo que, a√∫n
con sus asperezas, es una herramienta bastante buena. Sin embargo, como ya dec√≠a al principio,
me gustan mis dependencias controladas y descritas lo m√°s completamente posible. #nix es un viejo conocido en este sitio, y por descontado la mejor herramienta a d√≠a de hoy para encargarse de eso.

As√≠ que antes de escribir mi primera l√≠nea de Haskell me asegur√© de que mi entorno de desarrollo
fuese un [_flake_](./hispanix/2024-06-25-entornos-desarrollo.md) en condiciones.
Con el _flake_ pude establecer algunos aspectos que quer√≠a desde el minuto uno:

- La versi√≥n del compilador que me interesaba
(GHC 9.12.2, para probar las extensiones de [GHC2024](https://ghc.gitlab.haskell.org/ghc/doc/users_guide/exts/control.html#extension-GHC2024))
- Un formateador "√∫nico" para todos los tipos de fuentes del proyecto con
[`treefmt`](https://github.com/numtide/treefmt).
- _Pre-commit hooks_ para todas las comprobaciones que me interesaban,
incluyendo _hooks_ custom (`hlint`, `weeder`, `doctest`, etc).
- Un _shell_ de desarrollo con todas las dependencias listas (incluyendo formateador y _hooks_).

M√°s relevante que todos estos aspectos para comprender la potencia de Nix es el c√≥mo pude utilizarlo
para ejecutar los tests del [repositorio](https://github.com/munificent/craftinginterpreters) de
Crafting Interpreters.

El c√≥digo de los bancos de tests est√° escrito en Dart 2, y desde 2023 Dart est√° en la versi√≥n 3.
Dart 3 obliga a asegurar la _Null Safety_, cosa que los tests no hab√≠an hecho porque la versi√≥n 2 no
lo requiere, as√≠ que estos tests no funcionan con la √∫ltima versi√≥n. Si fuese un programador de Dart
y tuviese instalada la versi√≥n 3 tendr√≠a que pelear con mi entorno para instalar la versi√≥n 2 y
poder ejecutar los tests sin alterar mi entorno habitual.
Tal vez alguna herramienta tipo `nvm` para Dart, `asdf` o contenedores...

Pero como uso Nix, no tengo que preocuparme de este problema en absoluto. No tengo que conocer
herramientas espec√≠ficas para el lenguaje de turno ni arriesgarme a conflictos con herramientas
instaladas globalmente (un saludo, Python).

Busco Dart en el repositorio de paquetes m√°s grande y m√°s actualizado del mundo, `nixpkgs`. Busco
la revisi√≥n de `git` de `nixpkgs` que contenga la versi√≥n de Dart que me sirve. Fijo la revisi√≥n
como entrada a mi _flake_... y listo, **Dart 2.19.6** listo para ejecutar en mi terminal. Gracias a
`direnv`, en el momento en el que salgo del directorio del proyecto el ejecutable de `dart`
desaparece (o volver√≠a a la 3.9.4 si fuese un programador de Dart con una instalaci√≥n global).

De nuevo, tirando de las funciones de Nix escribo un _script_, tambi√©n en Haskell, (**_inline!_**)
que llama a Dart como si fuese un _script_ de Bash, de forma que puedo ejecutar los tests de cada
cap√≠tulo de forma aislada y pas√°ndole mi implementaci√≥n del int√©rprete.

¬øC√≥mo? mira esto (sacado del repo de [Shh](https://github.com/luke-clifton/shh#nix)):

```nix
writers.writeHaskellBin "example" { libraries = [ haskellPackages.shh ]; } ''
  {-# LANGUAGE TemplateHaskell #-}
  import Shh

  -- Load binaries from Nix packages. The dependencies will be captured
  -- in the closure.
  loadFromBins ["${git}", "${coreutils}", "${curl}"]

  main :: IO ()
  main = do
    cat "/a/file"
    cp "/a/file" "/b/file"
''
```

F√≠jate en el comentario del centro. Hasta la versi√≥n de las `coreutils` est√° fijada por Nix.

Es un nivel de control inigualable, no vuelves a mirar (ni a ejecutar) un _script_ de Bash igual.

Por supuesto, automatizar la ejecuci√≥n de estos tests en la CI (mismas versiones de programas, etc,
etc) es trivial. Esto da confianza absoluta: puedo saber si mi implementaci√≥n actual supera los
tests oficiales y seleccionar granularmente cu√°ntos y cu√°les cap√≠tulos compruebo, independientemente del lenguaje que haya escogido.

Escrib√≠ mi CI para que cada _pull request_ al repositorio implementando un cap√≠tulo
debiese pasar todos los tests de dicho cap√≠tulo. Nada de medias tintas.

Si quieres verlo, echa un vistazo a mi [`flake.nix`](https://github.com/DavSanchez/hox/blob/1eb3f09d00cf3f1c627bfddd2963441cb2dadef2/flake.nix#L48-L127).

## Escaneando: de la mutabilidad a la recursi√≥n de cola

El primer paso de cualquier int√©rprete es el esc√°ner (o _lexer_). En Java, Nystrom usa estructuras
habituales, `while`s que avanzan sobre la cadena de caracteres de entrada, mutando el estado en cada
iteraci√≥n (`while (!isAtEnd()) { start = current; ... }`).

¬øC√≥mo te aproximas a esto en Haskell, donde casi todo es inmutable y no hay estado? No es cuesti√≥n de tirar de `map` o `foldl` sobre listas de caracteres, porque
puedes necesitar m√°s que un car√°cter cada vez para determinar ante qu√© _token_ te
encuentras. Cuando llegas a `=` necesitas m√°s para saber si est√°s ante un **signo
de igual** o ante un **operador l√≥gico** `==`.

La respuesta a que no haya estado mutable es **llevar el estado contigo en la funci√≥n**, llam√°ndola recursivamente y pasando expl√≠citamente el estado
actualizado. ¬øTe suena lo de las dependencias
expl√≠citas?.

Este es el tipo de mi funci√≥n de escaneo en Haskell.

> [!info] _First time?_
> Si nunca has visto una firma de tipos de
> Haskell, asume que todos los tipos que aparecen excepto el √∫ltimo son par√°metros de la funci√≥n,
> y el tipo que aparece al final es el tipo de retorno.
>
> Para saber m√°s, lee sobre el [_currying_](https://en.wikipedia.org/wiki/Currying).

```haskell
-- | Scans the input strings and tracks the state: the current line number and the accumulated tokens
naiveScanTokens ::
  -- | Input string (a linked list of characters, `String` == `[Char]`)
  [Char] ->
  -- | Current line number
  Int ->
  -- | Accumulated tokens (my state!)
  [TokenResult] -> 
  -- | Resulting list of tokens, a non-empty list, can contain scanning errors
  NonEmpty TokenResult
```

> [!note] ¬øNa√≠f?
> La funci√≥n se llama `naiveScanTokens` porque inicialmente explor√©, aparte de
> esta versi√≥n literalmente escrita caso a caso seg√∫n le√≠a, otra versi√≥n utilizando
> _combinadores de parsers mon√°dicos_ (ya, ya. Sigue leyendo) y la biblioteca
> [`megaparsec`](https://hackage.haskell.org/package/megaparsec).
>
> Finalmente descart√© esta segunda opci√≥n por una cuesti√≥n did√°ctica a varios niveles:
>
> - Calentamiento despu√©s de un par de meses sin tocar Haskell.
> - No usar bibliotecas externas en la implementaci√≥n.
> - La secci√≥n de _parsing_ ya me permitir√≠a usar, tambi√©n manualmente, los combinadores.
> - Introducir algunos patrones de programaci√≥n funcional y Haskell a mis compa√±eros de equipo.
> - Escribir este art√≠culo alg√∫n d√≠a.

La implementaci√≥n simplemente hace _pattern matching_ sobre los caracteres de entrada y llama a la
misma funci√≥n actualizando el estado hasta que termina. F√≠jate en c√≥mo capturo cada car√°cter y c√≥mo
vinculo el resto de la lista a `ss` con el operador `:` en los argumentos de entrada, en el lado
izquierdo de la definici√≥n, para pasarla a la nueva llamada en el lado derecho, y c√≥mo en el caso
base a√±ado del estado de entrada (el argumento `tt`) el token `EOL` a la lista de salida (por eso
nunca est√° vac√≠a) con `:|`.

```haskell
-- We start with the base case: no more characters to process, I just add the EOF token
naiveScanTokens "" l tt = validToken EOF l :| tt
naiveScanTokens ('(' : ss) l tt = naiveScanTokens ss l (validToken LEFT_PAREN l : tt)
naiveScanTokens (')' : ss) l tt = naiveScanTokens ss l (validToken RIGHT_PAREN l : tt)
-- ...
-- Operators
naiveScanTokens ('!' : '=' : ss) l tt = naiveScanTokens ss l (validToken BANG_EQUAL l : tt)
naiveScanTokens ('!' : ss) l tt = naiveScanTokens ss l (validToken BANG l : tt)
-- ...
-- Whitespaces (no newlines)
naiveScanTokens (s : ss) l tt
  | s == ' ' || s == '\r' || s == '\t' =
      naiveScanTokens ss l tt
-- ... all other cases
-- If we reach here, it means we encountered an unexpected character
naiveScanTokens (s : ss) line tt = naiveScanTokens ss line (syntaxError "Unexpected character." line [s] : tt)
```

Tienes una funci√≥n definida varias veces seg√∫n los diferentes comportamientos que quieres.
Es como sobrecargar una funci√≥n por posibles sus valores de entrada.

El c√≥digo es casi una transcripci√≥n directa de las instrucciones del libro. Iba a√±adiendo los casos
a la funci√≥n seg√∫n los le√≠a, pero sin contadores manuales ni constructos de control
de flujo `while` o `for`. Llamada Datos y estado de entrada, transformaci√≥n, y
nueva llamada con nuevos datos y estado.

Solo hay que pasar un estado inicial (la primera l√≠nea y la lista vac√≠a de resultados) junto a la
entrada, que es lo que hago con `scanTokens` (esta solo tiene que aceptar el verdadero _input_):

```haskell
scanTokens :: String -> NonEmpty TokenResult
scanTokens s = reverse (naiveScanTokens s 1 [])
```

Puedes ver la implementaci√≥n completa de `naiveScanTokens` en mi repositorio ([`Scanner/Naive.hs`](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Scanner/Naive.hs#L100))

## _Visitor_ vs _Pattern Matching_ de tipos suma

La secci√≥n [_Working with trees_](https://www.craftinginterpreters.com/representing-code.html#working-with-trees)
del cap√≠tulo 5 explora las diferencias expresivas entre el paradigma orientado a
objetos y el paradigma funcional, y c√≥mo el patr√≥n de dise√±o _visitor_ trata de
acercar el estilo funcional al orientado a objetos.

Por supuesto, al acercarme al problema desde la programaci√≥n puramente funcional,
no iba a necesitar implementar el patr√≥n _visitor_. Como el propio libro menciona:

> Each kind of expression in Lox behaves differently at runtime. That means the
> interpreter needs to select a different chunk of code to handle each expression
> type. With tokens, we can simply switch on the TokenType. But **we don‚Äôt have a
> ‚Äútype‚Äù enum** for the syntax trees, just a separate Java class for each one.

¬°Nosotros s√≠ que tenemos ese _‚Äútype‚Äù enum_!

El sistema de tipos de Haskell es una de sus grandes fortalezas. En particular,
tenemos a nuestra disposici√≥n los **ADTs** (_Algebraic Data Types_), y su forma
de tipos suma (que tambi√©n se ve en los `enum` de Rust) cubre este caso con total
naturalidad, particularmente con la sintaxis de Haskell.

La gram√°tica formal para las expresiones de Lox empieza as√≠, sacada tal cual
del libro:

```txt
expression     ‚Üí literal
               | unary
               | binary
               | grouping ;
```

Y la primera versi√≥n de mi AST de expresi√≥n expresada como un tipo en Haskell
se ve√≠a as√≠:

```haskell
-- | Represents an expression in the AST.
data Expression = Literal Literal
                | Unary UnaryOperator Expression
                | Binary BinaryOperator Expression Expression
                | Grouping Expression
```

Hay informaci√≥n que falta en cada uno de los fragmentos, claro, pero imagino que
pillas la idea üòé.

El int√©rprete se reduce a una funci√≥n que encaja cada patr√≥n, como el esc√°ner,
pero esta vez sobre los constructores de variantes del tipo.
Es una soluci√≥n m√°s concisa que evita escribir un pu√±ado de clases sin m√©todos para
implementar _visitor_, y por supuesto te ahorra escribir el generador que el autor
tiene que fabricar para agilizar el a√±adido de dichas clases (son 21).

Puedes ver mi tipo de expresi√≥n actual en [`Syntax/Expression.hs`](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Syntax/Expression.hs).

## Parseando: combinadores de _parsers_ mon√°dicos

Esta secci√≥n creci√≥ demasiado, as√≠ que he decidido extraerla en su propio art√≠culo para darle la atenci√≥n que merece.

Si quieres saber c√≥mo funcionan los _parsers_ mon√°dicos, qu√© tienen que ver con `Result::and_then` en Rust y c√≥mo se combinan para crear estructuras complejas a partir de piezas simples, echa un vistazo a mi **[Introducci√≥n a los Combinadores de _Parsers_ Mon√°dicos](./2026-02-01-combinadores-parsers-monadicos.md)**

All√≠ explico en detalle c√≥mo pasamos de la idea de un esc√°ner manual a una abstracci√≥n mucho m√°s potente, declarativa y reutilizable.

## Codificando la distancia de resoluci√≥n de variables en el AST

Uno de los desaf√≠os m√°s interesantes fue el cap√≠tulo de **Resoluci√≥n de Variables**. En Lox, para manejar cierres (_closures_) correctamente, necesitamos saber a cu√°ntos "saltos" (_scopes_ l√©xicos) de distancia est√° definida una variable.

### La soluci√≥n Java: Side-Tables

En Java, el _Resolver_ recorre el √°rbol y guarda esta informaci√≥n en un `Map<Expr, Integer>` separado. El AST no cambia. Esto funciona en Java porque cada objeto `Expr` tiene identidad (direcci√≥n de memoria) y puede usarse como clave en un mapa.

### El problema en Haskell

En Haskell, los valores son inmutables y estructurales. `Variable "a"` es id√©ntico a otro `Variable "a"`. No tienen "identidad" por defecto. Para usar un `Map`, necesitar√≠a adjuntar IDs √∫nicos a cada nodo de forma incremental, lo que no escalar√≠a demasiado, o usar trucos inseguros como salir a `IO` para obtener uno aleatorio.

### Type-Driven Development

En lugar de una tabla lateral, decid√≠ que la informaci√≥n de resoluci√≥n deb√≠a vivir **dentro** del propio AST. Pero el `Parser` no conoce esa informaci√≥n en el momento en que emite nuestro AST. ¬øC√≥mo definimos un AST que a veces tiene resoluci√≥n y a veces no de forma segura?

Con esto de "segura" me refiero a que quiero validar est√°ticamente que mi AST est√° en el estado correcto en cada
etapa de su procesamiento. Es decir, mi _parser_ debe ser una funci√≥n de tipo (obviando posibilidad de errores y otras cosas cubiertas con anterioridad):

```haskell
parser :: [Token] -> UnresolvedAST
```

Y que, del mismo modo, la etapa de resoluci√≥n sea llevada a cabo por una funci√≥n `resolver` de tipo:

```haskell
resolver :: UnresolvedAST -> ResolvedAST
```

De forma que al int√©rprete real del programa solo pueda pas√°rsele el tipo `ResolvedAST`:

```haskell
interpret :: ResolvedAST -> Either RuntimeError ()
```

Esto es la seguridad de tipos o _type safety_, ninguna funci√≥n debe poder recibir un valor de un tipo que
no le corresponde. Nada de pasarle ASTs ya "resueltos" al `resolver`, o ASTs sin resolver al `interpret`.

Esto se podr√≠a lograr parametrizando el tipo de AST con un gen√©rico, pero entonces tendr√≠amos que preocuparnos
de que solo los gen√©ricos apropiados puedan servir como par√°metros del AST. Un simple `AST a` no vale, necesito
alg√∫n tipo de _restricci√≥n_ sobre `a`... Rust utiliza t√©cnicas como [_traits_ sellados](https://rust-lang.github.io/api-guidelines/future-proofing.html) para lograr esto, pero
en Haskell, gracias a su completo sistema de tipos, hay otras opciones.

Para expresar esto, utilic√© los conceptos de **Data Kinds** y **Type Families**.

#### ¬øQu√© es eso de _Kinds_?

He pasado de forma extremadamente superficial en [otro de mis art√≠culos](./2024-11-13-mapeando-estructuras.md), pero aqu√≠ ahondo un poco m√°s.

Puedes definir un tipo como **el conjunto de valores contenidos en ese tipo**:

- `Bool` es un tipo que contiene a los valores `True` y `False`.
- `Int` es un tipo que contiene a los valores enteros (..., `-2`, `-1`, `0`, `1`, `2`, etc).

Se puede decir que los valores (_terms_) pertenecen al _universo_ de sus tipos. `False` pertenece al
universo de `Bool`, `-1` al universo de `Int`, etc.

Los tipos, a su vez, tienen su propia clasificaci√≥n, subiendo un nivel de abstracci√≥n, llamada `Kind`.
En funci√≥n de c√≥mo de gen√©ricos son, los tipos est√°n englobados en sus propios _universos_ o `Kind`s.

- `Type` es el _universo_ (`Kind`) de los tipos b√°sicos.
  - `Bool` e `Int` son tipos dentro de este _universo_.
  - `Maybe Int` o `Either String ()` tambi√©n pertenecen a este _universo_.
- `Type -> Type` es el _universo_ de los tipos que tienen un par√°metro gen√©rico desconocido.
  - `Maybe` (¬°pero no `Maybe Int`!) pertenece a este _universo_.
  - `Either String` (¬°pero no `Either String ()`!) tambi√©n pertenece a este _universo_.
- `Type -> Type -> Type` es el _universo_ de los tipos que tienen dos par√°metros gen√©ricos desconocidos.
  - `Either` (sin especificar ninguno de sus par√°metros) pertenece a este _universo_.

Hay otros universos, y pueden combinarse de varias formas.

Sabemos que en Haskell puedes crear tus propios tipos. ¬øPodr√≠amos crear nuestros propios `Kind`s?
Si creo mi propio `Kind`, ¬øQu√© tipos viven dentro de √©l? ¬øQu√© valores tienen esos tipos?

Eso es permitido por lo que se conoce como **Data Kinds**. Cada vez que definimos un tipo, Haskell crea un `Kind` con el mismo nombre, y los constructores de valor del tipo original son como tipos, aunque no se pueden construir valores de estos.

```haskell
data MyBool   -- This is a type
  = MyTrue    -- This is a value constructor
  | MyFalse   -- This is another value constructor

{-
-- behind the scenes, it's like Haskell created the following:
kind MyBool    -- This is a Kind
  = MyTrue    -- This is a type!
  | MyFalse   -- This is a type!
-}
```

Pero si no puedo construir valores de estos extra√±os tipos promocionados a partir de un tipo convencional,
¬øpara qu√© sirven? Una respuesta f√°cil es **etiquetar otros tipos**, de forma que obtengo la restricci√≥n
que buscaba para el par√°metro gen√©rico de mi tipo AST.

```haskell
-- Definimos las fases de resoluci√≥n del AST
data Phase = Unresolved | Resolved

-- Haskell crea la `Kind` llamada `Phase` entre bambalinas.

-- El AST usa esta `Kind` como par√°metro, `p` solo puede ser `Unresolved` o `Resolved`!
data Expression (p :: Phase)
  = ...
```

A√∫n nos queda un detalle. Si este `Kind` no puede generar valores, y mi resoluci√≥n necesita esta
informaci√≥n din√°micamente (una variable puede estar definida 2 _scopes_ l√©xicos por encima, o 3,
o podr√≠a ser una variable global) ¬øC√≥mo la incorporamos al AST si hemos restringido los tipos gen√©ricos
a este `Kind`?

Aqu√≠ intervienen las **Type Families**.

#### **Type Families**

Una familia de tipos o _type family_ no es otra cosa que una funci√≥n de tipo a tipo. Como tal, solo
existe en tiempo de compilaci√≥n, pero eso no la hace ni mucho menos in√∫til.

Siendo que nuestro AST est√° parametrizado por un `Kind` que tiene tipos pero no puede tener valores
en tiempo de ejecuci√≥n, la _type family_ puede relacionar los tipos de este `Kind` con tipos que s√≠
puedan tener valores.

Siguiendo con nuestro ejemplo:

```haskell
-- Definimos las fases de resoluci√≥n del AST
data Phase = Unresolved | Resolved

-- Un tipo sin valores posibles, "zero-sized"
data NotResolved

-- Un tipo con valores que representa una resoluci√≥n concreta, global o local
data Resolution
  = Global -- Variable global
  | Local Int -- Variable local a X (Int) scopes de distancia

-- Haskell crea la `Kind` llamada `Phase` entre bambalinas.
-- Tambi√©n crear√≠a `Kind`s para `NotResolved` y `Resolution`, pero no nos interesan.

-- Una familia de tipos que cambia seg√∫n la fase
-- Funciones que reciben un tipo (de `Kind Phase`) y devuelven otro tipo (de `Kind Type`),
-- en este caso, los tipos definidos anteriormente
type family ResolutionInfo (p :: Phase) :: Type where
  ResolutionInfo 'Unresolved = NotResolved 
  ResolutionInfo 'Resolved = Resolution

-- El AST usa esta familia de tipos
data Expression (p :: Phase)
  = VariableExpr
      Int                -- L√≠nea
      String             -- Nombre
      (ResolutionInfo p) -- La type family es aplicada a `p`
  -- ...
```

La _type family_ es una funci√≥n, en tiempo de compilaci√≥n relaciona el `Kind` `Phase` con un tipo
concreto, de esta forma el tipo gen√©rico `p` de nuestro AST no puede ser cualquier cosa, solo lo que
nosotros definamos, ¬°y en tiempo de compilaci√≥n!

¬°Esto es **Type-Driven Development**!

1. El `Parser` produce un `Program 'Unresolved`.
2. El `Resolver` toma un `Program 'Unresolved` y devuelve un `Program 'Resolved`.
3. El `Interpreter` **solo** acepta `Program 'Resolved`.

```haskell
-- Interpreter.hs
programInterpreter :: Program 'Unresolved -> Interpreter ()
programInterpreter prog = do
  let (resolvedProg, errors) = runResolver (programResolver prog)
  if null errors
    then interpretProgram resolvedProg -- Aqu√≠ GHC sabe que es seguro
    else throwError (Resolve errors)
```

>[!note]
> F√≠jate en que las firmas de tipo mencionan los tipos del `Kind` `Phase` como `'Unresolved` y
> `'Resolved` respectivamente, con un ap√≥strofe delante. Esto es para desambiguar con los constructores
> de valores del tipo convencional `Phase`, que sigue pudiendo definir valores en tiempo de ejecuci√≥n
> aunque no se use para ello.

Si intento ejecutar un programa sin resolver, ¬°el c√≥digo ni siquiera compila! Es una garant√≠a de seguridad que otros lenguajes, ni siquiera Rust, pueden ofrecer tan f√°cilmente.

##### Un breve paso por los GADTs

Otra forma que explor√© para codificar esto fueron los **GADTs (Generalized Algebraic Data Types)**, una suerte de
tipos gen√©ricos cuyo par√°metro gen√©rico est√° determinado por su constructor de datos, pero finalmente la soluci√≥n
con **Data Kinds** y **Type Families** estaba m√°s alineada con lo que quer√≠a lograr.

## Gestionando el Estado: Restricciones por clases y MTL

En Java, el int√©rprete es una clase con campos mutables (`environment`). En Haskell, la mutabilidad y los efectos deben ser expl√≠citos. Mi implementaci√≥n utiliza la librer√≠a [**MTL (Monad Transformer Library)**](https://hackage.haskell.org/package/mtl) para construir una pila de efectos cuyo requerimiento es que cumplan la interfaz m√≥nada:

```haskell
newtype InterpreterT m a = Interpreter
  { runInterpreterT :: StateT (ProgramState Value) (ExceptT InterpreterError m) a
  }
  deriving newtype
    ( Functor,
      Applicative,
      Monad,
      MonadState (ProgramState Value),
      MonadError InterpreterError,
      MonadIO
    )

type Interpreter = InterpreterT IO -- concrete implementation
```

Visualmente, la pila de efectos se ve as√≠:

```mermaid
graph BT
    IO["IO (Base: Efectos de consola)"]
    ExceptT["ExceptT (Manejo de Errores)"] -->|envuelve| IO
    StateT["StateT (Entorno y Variables)"] -->|envuelve| ExceptT
    InterpreterT["InterpreterT (Newtype Wrapper)"] -->|envuelve| StateT
```

La capacidad de Haskell de generar las implementaciones de `Functor`, `Applicative`, `Monad` y
las clases de transformadores (`MonadState`, `MonadError`, `MonadIO`) autom√°ticamente, como se ve en ese `deriving newtype ...` hace muy sencillo trabajar con ellas.

Lo interesante aqu√≠ no es solo la pila de m√≥nadas ni que las implementaciones se generen autom√°ticamente, sino c√≥mo se utilizan. La mayor√≠a de las funciones del int√©rprete no conocen el tipo concreto sobre el que operan. En su lugar, utilizan **restricciones de clases de tipos** (_typeclass constraints_):

```haskell
evaluateExpr :: (
  MonadState (ProgramState Value) m,
  MonadError InterpreterError m
  ) => Expression 'Resolved -> m Value
```

Esta firma dice: "esta funci√≥n funciona en cualquier m√≥nada `m` que tenga un estado de tipo `ProgramState` y pueda lanzar errores de tipo `InterpreterError`". Esto desacopla la l√≥gica de la implementaci√≥n concreta (mi tipo `Interpreter`), facilitando los tests y permitiendo reutilizar funciones en diferentes contextos (como el `Resolver`, que comparte algunas de estas capacidades pero no todas, teniendo una estructura en cierto modo similar al `Interpreter`).

No es m√°s que inyecci√≥n de dependencias para efectos en Haskell, donde _efecto_ es algo tan general
como "puede fallar con este tipo de error", "gestiona estados de este tipo", etc.

Un detalle elegante es c√≥mo se manejan los _scopes_. En Java podr√≠amos usar `try-catch-finally` para asegurar que, al salir de un bloque, el entorno anterior se restaura. En Haskell, uso `catchError` (una funci√≥n que es
proporcionada por la clase `MonadError`, nada que ver con excepciones) junto con las primitivas de estado:

```haskell
executeBlock decls = do
  modify pushScope        -- Entrar en nuevo scope
  -- Si 'go decls' falla, ejecutamos el handler que hace popScope y relanza el error
  r <- catchError (go decls) (\e -> modify popScope >> throwError e)
  modify popScope         -- Salir del scope (caso exitoso)
  pure r
```

Haskell tambi√©n tiene excepciones, pero trato de no utilizarlas en favor de tratar lo m√°s posible a
los **errores como valores**.

## Errores como Valores

Una de las diferencias filos√≥ficas m√°s grandes de mi implementaci√≥n respecto a la del libro es el manejo de errores. En lugar de que el flujo de control salte m√°gicamente (con las exceptiones), los errores son valores (`Left InterpreterError`) que se propagan.

Aunque `ExceptT` o la implementaci√≥n mon√°dica de `Either b` hacen que parezca imperativo trabajar con errores (puedes hacer _short-circuiting_), su estatus de tipos de pleno derecho te obliga a ser consciente de en qu√© partes del c√≥digo pueden fallar las cosas y con qu√© valores pueden fallar. ¬°La firma de tipos lo indica!

Da una tranquilidad enorme saber que, en general, mirar una firma de tipos ya te dice si una funci√≥n puede fallar o no. Esto se extiende a saber si una funci√≥n es **pura** o interact√∫a de alguna forma con el _mundo exterior_ (cuando `IO _` aparece en el valor de retorno).

## Az√∫car Sint√°ctico: Pattern Synonyms

En mi implementaci√≥n del flujo de control y particularmente en los retornos de funci√≥n (esos `return true;`), de nuevo queriendo distanciarme del uso de excepciones para expresarlas, como hace la implementaci√≥n de Java (üò≠üíÄ) me encontr√© con un patr√≥n com√∫n. Necesitaba un tipo que representara "seguir ejecutando" o "detenerse y devolver un valor".

Podr√≠a haber usado `Either b c`, pero quer√≠a algo m√°s descriptivo, y estando familiarizado con [el tipo `ControlFlow` de Rust](https://doc.rust-lang.org/stable/std/ops/enum.ControlFlow.html) decid√≠ crear mi propia versi√≥n, sin que fuera nada del otro mundo:

```haskell
data ControlFlow b c
  = Break b     -- Detener el flujo con un valor (ej. return)
  | Continue c  -- Seguir ejecutando (tambi√©n con un valor, ¬øqui√©n sabe?)
```

Sin embargo, leer `Break` cuando estamos implementando una sentencia `return` me produc√≠a un poco de disonancia cognitiva, as√≠ que ech√© mano de otra de las herramientas de tipado de Haskell, los **Pattern Synonyms**. Me permiten crear un "constructor virtual" que hace de alias de uno existente:

```haskell
{-# LANGUAGE PatternSynonyms #-}

{-# COMPLETE Return, Continue #-}
data ControlFlow b c
  = Break b
  | Continue c
  deriving stock (Show, Eq)

-- | Pattern synonym for 'Break' to represent a return value in control flow.
pattern Return :: b -> ControlFlow b c
pattern Return x = Break x
```

A√±adiendo un pragma `COMPLETE` para que el chequeo de patrones del compilador considere que cubrir `Continue` y `Return` sea exhaustivo sobre todas las variantes del tipo `ControlFlow`, mi c√≥digo del int√©rprete puede usar `Return` como si fuera un constructor real, haciendo que la intenci√≥n sea muy clara:

```haskell
runFunctionBody ::
  ( MonadState (ProgramState Value) m,
    MonadError InterpreterError m,
    MonadIO m -- functions can print to screen, check the time, etc
  ) =>
  [Declaration 'Resolved] -> m Value
runFunctionBody [] = pure VNil
runFunctionBody (d : ds) =
  interpretDeclF d >>= \case
    Return v -> pure v -- yeah Return as Break!
    Continue () -> runFunctionBody ds
```

Es un peque√±o detalle, pero demuestra c√≥mo Haskell te permite modelar el dominio de tu problema no solo en los tipos, sino tambi√©n en la sintaxis misma. ¬øEs sobre-ingenier√≠a para este int√©rprete? Totalmente. Pero parte de la diversi√≥n de usar un lenguaje as√≠ de expresivo en el tipado (y en general) es ver hasta d√≥nde puedes llegar con √©l.

## Testeo de propiedades

Adem√°s de la _suite_ oficial de tests de Lox, que son casos de ejemplo espec√≠ficos que analizan la salida de tu programa, lo que no te permite saber _d√≥nde_ tienes el _bug_, aprovech√© el ecosistema de Haskell para a√±adir **_Property-Based Testing_** con [`QuickCheck`](https://hackage.haskell.org/package/QuickCheck), una utilidad de test muy conocida, que utiliz√© desde el _framework_ [`tasty`](https://hackage.haskell.org/package/tasty).

En lugar de escribir `assert(scan("((") == [LEFT_PAREN, LEFT_PAREN])`, defino propiedades universales que mi c√≥digo debe cumplir para _cualquier_ entrada generada aleatoriamente. Por ejemplo, mi esc√°ner debe cumplir siempre que:

1. La lista de _tokens_ resultante nunca puede ser m√°s larga que la cadena de entrada (m√°s uno por el EOF).
2. El escaneo siempre termina en un _token_ `EOF` o en un error de "String no terminado".

```haskell
-- test/Scanner/Props.hs
scannerProperties :: TestTree
scannerProperties = 
  testGroup "Scanner Property Tests"
    [ testProperty "Always ends in EOF or an unterminated string error" eofOrUnterminatedString
    , testProperty "Less or equal tokens than input length" lessOrEqualTokensThanInputLength
    ]
```

Es una forma limitada de _fuzz testing_ que comprueba una propiedad en tus funciones generando muchos par√°metros aleatorios, pas√°ndolos a tu funci√≥n repetidas veces y, por medio de t√©cnicas de _shrinking_, mostr√°ndote el m√≠nimo ejemplo donde tus invariantes no se cumplen si lo hay. Es ideal para encontrar esos _casos esquineros_ (como cadenas vac√≠as, o llenas de caracteres nulos) que a veces se nos pasa cubrir con tests unitarios convencionales.

## Conclusiones

Implementar Lox en Haskell ha sido un ejercicio de traducci√≥n cultural.

- Donde OOP ve **Comportamiento encapsulado con Datos**, FP ve **Datos puros y Funciones transformadoras**.
- Donde OOP usa **Identidad de Objetos**, FP usa **Tipos Estructurales**.
- Donde OOP usa **Excepciones y Mutabilidad**, FP usa **M√≥nadas y Transformadores**.

Arquitecturas como la **Resoluci√≥n basada en Tipos** me han mostrado un poder de expresividad y seguridad que dif√≠cilmente quiero abandonar para volver al mundo de `void foo()`. Haskell es una herramienta inmensamente poderosa que no solo resuelve problemas, sino que afila la mente del programador en el proceso.

El c√≥digo completo est√° disponible en el repositorio.

## Siguientes pasos

### Rendimiento

Mi implementaci√≥n es una traducci√≥n en algunos casos equivalente a la de Java que iba siguiendo, en otros casos necesitada de enfoques alternativos.

Cuando la secci√≥n del _tree-walk interpreter_ daba paso a la secci√≥n de la _bytecode virtual machine_ el autor de Crafting Interpreters propone ver cu√°nto tarda en nuestra implementaci√≥n la cl√°sica implementaci√≥n mala del c√°lculo de sumas de n√∫meros de Fibonacci:

```lox
fun fib(n) {
  if (n < 2) return n;
  return fib(n - 1) + fib(n - 2); 
}

var before = clock();
print fib(40);
var after = clock();
print after - before;
```

¬°Huelga decir que el rendimiento de mi implementaci√≥n deja bastante que desear!

La elecci√≥n de determinadas estructuras de datos, por ejemplo las listas enlazadas tan ubicuas en
Haskell, o la evaluaci√≥n perezosa por defecto pueden hacer dif√≠cil analizar tus cuellos de botella
con claridad, as√≠ que una de las pr√≥ximas √°reas de trabajo es mejorar en este aspecto.

#### _Benchmarks_ en CI

No se puede mejorar lo que no se mide. Para asegurarme de que mis cambios futuros realmente mejoren el
rendimiento del int√©rprete, o al menos no lo empeoren, integr√© una suite de **benchmarks** utilizando `tasty-bench`.

Eleg√≠ `tasty-bench` sobre el cl√°sico `criterion` por su integraci√≥n natural con el resto de mi infraestructura de tests, que ya estaba basada en el ecosistema de `tasty`. Utilic√© como _benchmark_ inicial para int√©rpretes es la funci√≥n de Fibonacci de antes, que estresa la resoluci√≥n de variables, las llamadas a funci√≥n y las operaciones aritm√©ticas b√°sicas.

Pero ejecutar benchmarks en mi m√°quina no es suficiente. Quer√≠a visualizar la tendencia hist√≥rica. Para ello, escrib√≠ un _workflow_ de **GitHub Actions** que:

1. Ejecuta los benchmarks en Linux (x64 y ARM) y macOS.
2. Convierte los resultados a JSON usando un peque√±o script de Python.
3. Utiliza la acci√≥n `benchmark-action/github-action-benchmark` para generar gr√°ficas de evoluci√≥n y notificarme si un _commit_ introduce una regresi√≥n de rendimiento superior al 150%.

Esto convierte el rendimiento en una propiedad observable del proyecto, a la espera de que haga algo por mejorarla.

### La _bytecode virtual machine_, en Rust

Como ya mencion√©, el libro _Crafting Interpreters_ tiene una segunda parte: una m√°quina virtual de _bytecode_ escrita en C (`clox`). Esta vez todos los compa√±eros del club de lectura hemos estado de acuerdo en utilizar **Rust**, pero ya que lo utlizamos diariamente en su forma _segura_, aprovecharemos para seguir los detalles de la implementaci√≥n en C, sumergirnos de lleno en √°mbitos `unsafe` y crear nuestras propias estructuras de datos desde cero (nuestros propios _arrays_ din√°micos en lugar de usar `Vec`, etc). Ya veremos cuando lleguemos a la secci√≥n de _garbage collection_... ¬°Tiene pinta de que lo vamos a pasar especialmente "bien"!

Al momento de escribir este art√≠culo apenas estamos empezando, pero puedes seguir el desarrollo de mi implementaci√≥n (llamada, no muy originalmente, `rox`) [aqu√≠](https://github.com/DavSanchez/rox).

Veamos si el repaso del paradigma funcional puro que segu√≠ en `hox` puede ayudarme en `rox` con _unsafe_ Rust.
Como m√≠nimo, seguro que sigo d√°ndole la vara a mis compa√±eros con la programaci√≥n guiada por tipos.

¬°Hasta otra!
