---
title: "Introducci칩n a los Combinadores de Parsers Mon치dicos"
short-title: "Combinadores de Parsers Mon치dicos"
date: 2026-02-01
page:
  image: blog/img/monadic-parser-combinator-legos.png
tags:
  - haskell
  - fp
  - tech
  - parsing
  - type-driven development
  - 游쀯릖
---
<!-- LTeX: language=es -->

![Los combinadores unen cada parser como piezas de lego](./img/monadic-parser-combinator-legos.png)

> [!info] Contexto
> Este art칤culo nace de una secci칩n que creci칩 demasiado en mi [art칤culo sobre implementar Lox en Haskell](./2026-02-01-lox-en-haskell.md).
>
> Aqu칤 exploro c칩mo los combinadores permiten construir _parsers_ mon치dicos complejos componiendo piezas simples, y c칩mo el concepto de m칩nada ya aparece en cierta forma en Rust.

Este art칤culo tiene un poco de miga, pero creo que muestra la verdadera potencia de Haskell y la programaci칩n funcional a quienes no la conocen.

La versi칩n de Java del libro **Crafting Interpreters** hace uso del descenso recursivo para implementar el _parser_. Aunque la idea es similar, yo iter칠 sobre el patr칩n del esc치ner y apret칠 un poco m치s la tuerca usando unas estructuras bastante potentes, los combinadores de _parsers_ mon치dicos.

Veamos de qu칠 van.

## 쯈u칠 es un _parser_?

Un _parser_, al fin y al cabo, no es m치s que una **funci칩n** que acepta una entrada y devuelve una salida. Pero, 쯤u칠 salida?:

```haskell
parser :: Input -> ?
```

En una versi칩n muy simple, podemos tratar de procesar toda esa entrada y devolver nuestro resultado, que puede ser o bien tu tipo de salida (e.g. `Expression`) o un error (quiz치 pasamos una secuencia de _tokens_ sin ning칰n sentido).

Como estamos en el mundo puro y matem치tico no queremos utilizar algo como excepciones que haya que capturar luego o dejar que maten al programa, aunque podemos. En la programaci칩n funcional pura y est치ticamente tipada, **los errores tambi칠n son valores**, y el tipo de las funciones contempla el tipo de errores que pueden emitir.

Para codificar esto en la signatura de nuestra funci칩n hacemos uso, por supuesto, de ADTs. Un tipo con dos variantes, el 칠xito y el fallo:

```haskell
data ParseResult = Ok Output
                 | Err ParseError
```

Generalizamos a cualquier tipo y obtenemos:

```haskell
data ParseResult e a = Ok a
                     | Err e

-- Parameterizing with our types
type ParseResultAlias = ParseResult ParseError Output
```

Por supuesto, Haskell tiene ya tipos para representar esto. Tal y como Rust usa su tipo [`Result<T, E>`](https://doc.rust-lang.org/std/result/), aqu칤 usamos `Either e a`.

> [!info]
> Rust y Haskell usan un orden distinto para los par치metros de tipo en `Result` y `Either`. Esto es porque en Haskell nos suele interesar el poder _mapear_ sobre el tipo que queda a la derecha.
>
> Puedes leer un poco al respecto en [este otro art칤culo](./2024-11-13-mapeando-estructuras.md).

As칤 que el tipo de mi funci칩n _parser_ podr칤a tener esta forma, generalizando sobre el tipo de entrada (que llamamos `s`), el tipo de salida (que llamamos `a`) y el tipo de error (que llamamos `e`):

```haskell
parser :: s -> Either e a
```

## 쮺칩mo se _combinan_?

Pero est치bamos hablando de **combinadores**. 쯈u칠 es lo que combinamos? La respuesta es piezas de _parser_ (recuerda, funciones) m치s peque침as.

Pero si una funci칩n que consume toda la entrada es a su vez una combinaci칩n de _sub-funciones_, no tendr칤a demasiado sentido que las funciones peque침as consumieran toda la entrada. De ser as칤, solo una de ellas podr칤a consumir la entrada, con el resto de las _sub-funciones_ operando sobre la salida de esta, y la combinaci칩n de la que hablamos ser칤a simplemente una composici칩n directa de funciones (e.g. `subParser3(subParser2(subParser1(input)))`).

Si contemplamos m치s opciones que esta, entonces las _sub-funciones_, en general, no deben consumir toda la entrada.

Entonces, 쯖칩mo expresamos que la entrada no ha sido consumida al completo en nuestras funciones?

Pues as칤:

```haskell
parser :: s -> Either e (a, s)
```

Ahora la salida del caso de 칠xito de mi funci칩n es una _**tupla**_ que contiene dos tipos, el tipo de salida **y el resto de la entrada que falta por procesar**.

> [!info]
> En mi implementaci칩n [el tipo `Parser` no es exactamente as칤](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Parser.hs#L25-L27) debido a su capacidad de [sincronizaci칩n y recuperaci칩n de errores](https://www.craftinginterpreters.com/parsing-expressions.html#panic-mode-error-recovery), pero no necesitamos cubrir ese aspecto para entender la combinaci칩n de _parsers_ mon치dicos en su forma b치sica.

Aqu칤 est치 la forma final que tendr칤a un _parser_ totalmente gen칠rico, encapsulado en una estructura que engloba un 칰nico tipo de valor (la funci칩n de _parsing_ en s칤) y un alias para los tipos concretos con los que vamos a trabajar:

```haskell
newtype Parser e s a = Parser
  { runParser :: s -> Either e (a, s)
  }

data ParseError = ParseError String

-- Parameterizing the input and the error, but not the output!
type TokenParser a = Parser ParseError [Token] a
```

Entonces, estos combinadores ir치n consumiendo la entrada (`[Token]`) y devolver치n el resultado (que puede ser o bien un tipo cualquiera `a` o un `ParseError`).

Asumiendo una secuencia de _tokens_ que 칰nicamente pueda formar una expresi칩n v치lida, una combinaci칩n de estos _parsers_ recibir칤a la secuencia completa y acabar칤a con el valor `Right (Expression, [])`. El resultado con variante de la derecha (칠xito) con mi valor de tipo `Expression` y la lista de _tokens_ consumida al completo.

> [!tip] 춰Solo es un esc치ner!
> Es importante que te quedes con la idea de que esto no es muy diferente a lo que hice con el esc치ner, pero subiendo un poco el nivel de abstracci칩n.
>
> Un esc치ner no es otra cosa que una funci칩n `[Char] -> Either SyntaxError ([Token], [Char])` que consume toda la entrada de golpe, pero las llamadas recursivas intermedias s칤 que pasan "lo que queda de la lista de caracteres" y a su vez [emit칤an o un `validToken` o un `syntaxError`](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Scanner/Naive.hs#L12-L18)... 游땔.

Llegados a este punto, 쮺칩mo combinamos estos _parsers_ para generar mi `TokenParser Expression`?

## La palabra que falta

Podemos darle a nuestro tipo `Parser e s a` la capacidad de realizar cierta operaci칩n de encadenado con otros parsers de distintos tipos, de forma que cada _parser_ consuma un poco de la entrada, **y luego** el siguiente _parser_ consuma otro poco, y as칤 hasta cuanto queramos. Nosotros determinar칤amos cu치ndo termina la cadena y qu칠 tipo esperamos a la salida. Idealmente, la cadena terminar칤a cuando no queda m치s entrada. Por supuesto, si el proceso de _parse_ falla,
simplemente detenemos toda la operaci칩n y emitimos el fallo.

Apoy치ndonos en Rust por familiaridad, podemos buscar alguna funci칩n que se parezca a lo que buscamos.

Y _resulta_ (xd) que tenemos un ["**y luego**"](https://doc.rust-lang.org/std/result/enum.Result.html#method.and_then) que se parece bastante a lo que buscamos, entre los m칠todos de `Result<T, E>`:

```rust
/// Calls `op` if the result is `Ok`, otherwise returns the `Err` value of `self`.
///
/// This function can be used for control flow based on `Result` values.
pub fn and_then<U, F>(self, op: F) -> Result<U, E>
where
    F: FnOnce(T) -> Result<U, E>,
```

`and_then` recibe como par치metro un valor `Result<T, E>` (ese `self`) y una funci칩n `FnOnce(T) -> Result<U, E>` (ese `op`), devolviendo el tipo `Result<U, E>`. Es decir, **encadena un resultado con 칠xito** con una **funci칩n que devuelve otro resultado (con 칠xito o sin 칠l)**. Si el primer resultado no tuvo 칠xito, devolvemos el error directamente, sin encadenarlo con la funci칩n `op`.

Existe un equivalente en Haskell, y el sistema de tipos nos permite expresar esta capacidad de encadenamiento de una forma tan abstracta que, usando la terminolog칤a de orientaci칩n a objetos por familiaridad, es el equivalente al m칠todo de una interfaz. Haskell llama a estas formas de reusar c칩digo **clases de tipo** (no confundir con las clases en orientaci칩n a objetos) o _typeclass_. En Rust, el equivalente son los _traits_.

Esta clase de tipo, por fin, es la clase [`Monad`](https://hackage-content.haskell.org/package/base-4.22.0.0/docs/Prelude.html#t:Monad). De ah칤 lo de _parsers mon치dicos_.

F칤jate en el 칰nico m칠todo (o m치s bien operador) que debe definir un tipo para implementar esta clase `Monad`:

![Definici칩n m칤nima de la clase Monad](./img/hs_monad_definition.png)

> Compone dos acciones secuencialmente, pasando cualquier valor producido por la primera como argumento de la segunda.

Este `>>=` es un operador que une dos argumentos, un valor `m a` (el resultado de la primera acci칩n) y una funci칩n `(a -> m b)` (la segunda, que depende del resultado exitoso de la primera).

Te ayudo a unir Haskell y Rust usando una suerte de 치lgebra de tipos.

- `m _` es `Result<_, E>`.
- `a` es `T`, por tanto `m a` es `Result<T, E>`.
- `b` es `U`, por tanto `m b` es `Result<U, E>`.
- Por tanto, `a -> m b` es `FnOnce(T) -> Result<U, E>`
  - Obviemos _ownership_, sem치nticas de movimiento de valores, etc, y aceptemos ese `FnOnce` como
_una funci칩n cualquiera que acepte un `T` y devuelva un `Result<U, E>`_.

```haskell
-- Haskell       -- Rust
(>>=)            -- fn and_then(
  :: m a         --   self: Result<T, E>,
  -> (a -> m b)  --   op: FnOnce(T) -> Result<U ,E>
  -> m b         -- ) -> Result<U, E>
```

**춰Son lo mismo!** La diferencia es que en Rust no es f치cil expresar ese `and_then` para ciertos
grupos de tipos como parte de un _trait_, como en su momento exploramos en mi
[anterior art칤culo](./2024-11-13-mapeando-estructuras.md), y por ello es un m칠todo propio de `Result<T, E>`
(y, por lo mismo, tambi칠n est치 definido separadamente para `Option<T>`).

> [!warning]
> Implementar este _trait_ o interfaz `Monad` de cualquier manera no es suficiente
> para disfrutar de sus capacidades. Como estructura derivada de una formalizaci칩n matem치tica,
> la implementaci칩n **ha de cumplir ciertas leyes**, algunas de las cuales Haskell codifica en
> otros _traits_ como [`Functor`](./2024-11-13-mapeando-estructuras.md) y `Applicative`.
>
> Adem치s, la estructura debe tener cierta _forma_ (resumiendo, debe ser gen칠rica sobre otro tipo).

La magia de este operador es c칩mo representa la idea que discut칤amos al principio: c칩mo componer
acciones peque침as en una acci칩n m치s grande. Y puede que al principio no se vea, pero esta operaci칩n
fundamental encarnada en `>>=` est치 detr치s de muchos patrones habituales en programaci칩n. Encadenar
operaciones en un _contexto com칰n_:

- El t칤pico _short-circuiting_: Ejecuta funciones falibles una detr치s de otra, pero al primer fallo
que encuentres falla toda la operaci칩n y no contin칰es.
- Funciones que pueden retornar un _nulo_ y funciones posteriores que necesitan que se haya devuelto
un valor para ejecutar.
- Funciones as칤ncronas (`async/await`).
- etc.

## Y bien, 쯤u칠 hemos ganado?

Quiz치 te hayas fijado en c칩mo detr치s de las clases de tipo de Haskell se hallan operaciones
fundamentales que representan de forma muy abstracta c칩mo los tipos y las funciones se pueden
combinar entre s칤, sin por ello conocer nada sobre los tipos concretos m치s all치 de imponer ciertas
reglas sobre su comportamiento y su _forma_.

A cambio de estos requerimientos (o gracias a ellos), obtienes una serie de operadores gracias a los
cuales puedes componer peque침os _parsers_ para crear _parsers_ mayores, hasta que ensamblas el _parser_
final para tu lenguaje al completo.

Algunos de estos operadores y su funci칩n son los siguientes:

- `<$>`: Esto no es m치s que el operador de la t칤pica funci칩n `map` que vemos en otros lenguajes.
  Sirve para aplicar una funci칩n `a -> b` a nuestra estructura parametrizada en `a` de forma que
  obtengamos un valor de nuestra estructura parametrizada en `b`. Por ejemplo, si tengo un valor
  de tipo `TokenParser Int` de nombre `myIntParser` puedo obtener un `TokenParser String` haciendo
  `toString <$> myIntParser`.
- `<*>`: Es `<$>` pero de forma que las estructuras con las que operamos est칠n parametrizadas por
  funciones. Por ejemplo, si tengo un `unaryOperator :: TokenParser (a -> a)` (por ejemplo para
  el token `!` que representara la negaci칩n l칩gica), podr칤a aplicar la funci칩n que est치 dentro del
  `TokenParser` (es decir, mi `a -> a`) a un `TokenParser` parametrizado sobre un tipo que pueda
  servir como par치metro para la funci칩n. Por ejemplo, `unaryOperator <*> boolParser` si
  `boolParser :: TokenParser Bool`. Es una forma de secuenciaci칩n de operaciones algo menos general
  que `>>=`.
- `*>`: Aplica la funci칩n y luego descarta el valor del primer argumento. Esto es 칰til si nos
  interesa que la primera aplicaci칩n no haya fallado, pero no nos interesa su valor. Si con esto no
  lo ves del todo claro, mira los ejemplos a continuaci칩n.
- `<*`: Lo mismo, pero descartando el valor del segundo argumento.

As칤 listados puede que no parezcan muy 칰tiles, pero si los combinamos apropiadamente su potencia
queda al descubierto. Antes de mostr치rtelo con ejemplos, veamos otras dos utilidades que Haskell
pone a nuestra disposici칩n, una en sintaxis y otra con otra clase de tipo.

## Legibilidad imperativa con la _notaci칩n `do`_

Si tuvi칠ramos que escribir un parser que leyera tres tokens consecutivos (imagina que esperando
encontrar un `Expression` cada vez) usando `>>=`, se ver칤a as칤:

```haskell
parseThreeTokens :: TokenParser [Expression]
parseThreeTokens =
  token >>= \t1 ->
    token >>= \t2 ->
      token >>= \t3 ->
        pure [t1, t2, t3]

token :: TokenParser Expression
token = undefined -- assume a proper implementation
```

Esto no es otra cosa que el viejo _callback hell_ que se padec칤a en JavaScript. Las operaciones de
v칤nculo mon치dico `>>=` se van anidando y el c칩digo se vuelve dif칤cil de leer.

Haskell ofrece un poco de "az칰car sint치ctico" para trabajar con m칩nadas: la notaci칩n `do`.
El c칩digo anterior se puede reescribir as칤:

```haskell
parseThreeTokens = do
  t1 <- token
  t2 <- token
  t3 <- token
  pure [t1, t2, t3]
```

춰Parece c칩digo imperativo! "Lee un token y gu치rdalo en t1, luego lee otro en t2...".
Pero por debajo, el compilador lo traduce a las llamadas encadenadas de `>>=` que vimos antes.

## Alternativas: Esto o Aquello

Un _parser_ no es solo una secuencia de pasos ("primero esto, _y luego_ esto"). A veces necesitamos opciones ("intenta esto, _y si no funciona_, intenta esto otro").

Por ejemplo, una expresi칩n podr칤a ser un n칰mero literal _o_ una cadena de caracteres _o_ una expresi칩n agrupada entre par칠ntesis.

Para esto existe otra clase de tipos llamada `Alternative`. Define un nuevo operador `<|>` que
representa esta elecci칩n. Si intentamos un _parser_ y fallamos, devolvemos el segundo. Por supuesto,
estas operaciones son asociativas (`Alternative` se define como "a monoid on applicative functors"),
as칤 que podemos encadenar varios de estos _parsers_ y devolver el primero que haya funcionado. 춰Y
sin haber consumido la lista de _tokens_ en cada intento!

## El ejemplo para ilustrar todo

춰Por fin!

Imagina que empezamos con una funci칩n de tipo `TokenParser` que solo valide si
un predicado respecto a un `Token` se cumple. Como todos los _parsers_ que
hemos modelado hasta ahora, `TokenParser a` es un `Parser` que recibe una lista de `Tokens` (`[Token]`),
emite errores de tipo `ParseError` (cuya forma no hemos explorado) y devuelve un tipo `a`.

Como `satisfy` solo se encarga de fallar si el predicado no se cumple, ser치 un `TokenParser Token`,
porque no fabrica un nuevo valor a partir del `Token` de entrada:

```haskell
satisfy :: (Token -> Bool) -> TokenParser Token
satisfy predicate = Parser $ \case
  (t : tt) | predicate t -> Right (t, tt)                   -- if the predicate holds, we return success (Right)
  (t : tt) -> Left (ParseError "predicate not satisfied")   -- just an example of constructing an error
  [] -> Left (ParseError "no tokens to parse")
```

Ahora imagina que tenemos un _parser_ de operadores unarios (`!` para la negaci칩n l칩gica o el signo
`-` para negar un n칰mero). Imagina que podemos tener un _parser_ para cada una de las opciones.

En cada uno de estos _parsers_, podemos hacer uso de `satisfy` para validar si tenemos el _token_
adecuado para representar esta operaci칩n (es decir, si estamos ante el _token_ de un `!` o de un `-`):

```haskell
-- Data type representing the operator itself
data UnaryOperator = Bang | Negation

-- With "do notation"
parseBang :: TokenParser UnaryOperator
parseBang = do
  -- I pass the boolean function to `satisfy` but bind it to `_`
  -- because I don't care about the token itself
  _ <- satisfy (\t -> t == '!')  
  pure Bang -- puts a value into the structure (this comes from `Applicative`!)

-- Or, with the combinator operators...
parseBang' :: TokenParser UnaryOperator
parseBang' = satisfy (\t -> t == '!') *> pure Bang

--- then, `parseNegation`, etc
```

춰Si fallamos en `satisfy`, el _parser_ completo falla! Aunque la primera versi칩n de `parseBang` parezca
m치s natural, f칤jate en la capacidad expresiva de la versi칩n utilizando los operadores que combinan
un _parser_ con otro.

쯈uieres m치s? Combinemos los dos _parsers_ necesarios para cada operador unario en un 칰nico _parser_
de operaciones unarias:

```haskell
parseUnaryOperation :: TokenParser UnaryOperator
parseUnaryOperation = parseBang <|> parseNegation
```

Hab칤amos dicho que las operaciones eran asociativas, por lo que cabe expresar algo como:

```haskell
-- | Comparison chain parser (<, <=, >, >=).
parseComparison = parseGT <|> parseGTE <|> parseLT <|> parseLTE
```

쯈u칠 pasa cuando hayamos creado un _parser_ de `Expression` y nos falte la versi칩n de una expresi칩n
agrupada entre par칠ntesis? Con los operadores que descartan argumentos la expresividad es alt칤sima:

```haskell
parseGrouping :: TokenParser Expression
parseGrouping = do
  _ <- satisfy (\t -> t == '(')
  expr <- parseExpression
  _ <- satisfy (\t -> t == ')')
  pure (Grouping expr)

parseGrouping' :: TokenParser Expression
parseGrouping' = leftParen *> (Grouping <$> parseExpression) <* rightParen
  where
    leftParen = satisfy (\t -> t == '(')
    rightParen = satisfy (\t -> t == ')')
```

Con estos dos superpoderes (secuenciaci칩n con `do` y elecci칩n con `<|>`), el c칩digo del parser de Lox en Haskell se parece sorprendentemente a la gram치tica formal del lenguaje.

Aqu칤 tienes un ejemplo real de mi implementaci칩n para parsear una expresi칩n unaria (`!true` o `-5`) donde
s칤 utilizamos el _token_ que hemos capturado en `satisfy`:

```haskell
unary :: TokenParser Expression
unary =
  ( do
      -- Try to find an unary operator ('!' or '-'), bind it to `operator`
      operator <- satisfy (\t -> t `elem` ['!', '-'])
      -- AND THEN (`>>=`) recursively parse the following as an expression
      rightSide <- unary
      -- Emit the AST node combining all components (where `operator` is of type `Token`)
      pure (Unary operator rightSide)
  )
    -- If we fail above, try with the next level of precedence (function call)
    <|> call
```

Es declarativo, limpio y maneja los errores y el estado (la lista de _tokens_ restantes) autom치ticamente "entre bastidores" gracias a la clase de tipo m칩nada y a todos los operadores que los requisitos de esta clase impuso sobre nuestro tipo.

Comparado con la implementaci칩n t칤pica de descenso recursivo imperativo, donde tienes que comprobar manualmente `match(TOKEN)` y avanzar el puntero, esto se siente como describir **qu칠** es el lenguaje en funci칩n de las piezas primitivas que lo componen, en lugar de **c칩mo** leerlo.

## Conclusi칩n

Los combinadores de _parsers_ mon치dicos son un ejemplo claro de c칩mo estas abstracciones matem치ticas
reducen los problemas a sus elementos primordiales y a c칩mo se relacionan (componen) entre s칤,
llevan a c칩digo m치s expresivo, f치cil de componer desde esas piezas peque침as a piezas m치s grandes y
tambi칠n, una vez te acostumbras, 춰muy legible a su manera!

- **`Functor` (`<$>`)**: Nos permite transformar el valor producido sin alterar la estructura.
- **`Applicative` (`pure`, `<*>`, `*>`, `<*`)**: Nos permite secuenciar pasos independientes y combinar sus resultados.
- **`Monad` (`>>=`, `do`)**: Nos permite secuenciar pasos dependientes escondiendo la gesti칩n del estado, errores y otros.
- **`Alternative` (`<|>`)**: Nos permite expresar reglas gramaticales con opciones de forma natural.

Al final, escribir un _parser_ en Haskell se parecen menos a escribir una m치quina de estados compleja y m치s a escribir la gram치tica EBNF directamente en tu editor. La programaci칩n funcional pura y est치ticamente tipada favorece la expresi칩n declarativa. El _qu칠_ antes que el _c칩mo_.

### Mi implementaci칩n de `Monad`, `Alternative` y sus requisitos

Puedes ver un ejemplo aproximado de la pinta que tendr칤an las implementaciones de clases para
nuestro hipot칠tico _parser_ en mi [implementaci칩n real para Lox](https://github.com/DavSanchez/hox/blob/857185920c3f2c805109916c08676ce508d91bef/src/Language/Parser.hs).

Como ya indiqu칠, mi _parser_ es un poco diferente en su
estructura porque quiero conservar el estado si el _parsing_ falla, pero la implementaci칩n no var칤a
demasiado.

Si te atreves, intenta partir de mi implementaci칩n, cambiar el tipo para que se parezca
al de este art칤culo, y luego modificar las implementaciones de las clases de tipo. El sistema de
tipos de Haskell es bastante potente y te puede ayudar a alcanzar la forma en que encajan todas las
piezas.

>[!tip]
> Algo extremadamente relevante en los tiempos que corren es que un sistema de tipos robusto como el
> de Haskell es ideal para asistirte con IA, porque el compilador no va a aceptar cualquier c칩digo.

춰Hasta la pr칩xima!
