---
title: "Implementando Lox en Haskell"
short-title: "Lox en Haskell"
date: 2026-01-20
page:
  image: blog/img/functor-diagram.png
tags:
  - haskell
  - fp
  - tech
  - üá™üá∏
---
<!-- LTeX: language=es -->

> [!info] O, en resumen,
> _Qu√© tal estuvo implementar un lenguaje de programaci√≥n orientado a objetos basado en clases
> usando **programaci√≥n funcional pura y est√°ticamente tipada**._
>
> ![Jean-Baptiste Emmanuel Zorg diciendo "mi favorito".](https://camo.githubusercontent.com/c553f4c74ab9dba08d78c9511cfd7a4fd19dd4047ac86e608ea581d3526778be/68747470733a2f2f6d656469612e74656e6f722e636f6d2f584a77773370595a59394d41414141432f7a6f72672d6d792d6661766f75726974652e676966)
>
> Transparencia referencial, inmutabilidad, evaluaci√≥n perezosa, programaci√≥n por tipos, m√≥nadas,
> c√°lculo lambda y otros amigos.

Siempre me atrajo la programaci√≥n funcional propuesta por **Haskell**.

Programar utilizando propiedades matem√°ticas, de forma declarativa y con las dependencias
expl√≠citas, ver c√≥mo encajan todas las piezas (y el compilador acepta el programa) cuando tu modelo
de tipos se ajusta al problema, notar c√≥mo tu entendimiento mejora en el proceso.

Otra historia.

Como tambi√©n me pasa con **Nix**, toda aquella pieza de tecnolog√≠a con estos principios ‚Äîa menudo
tras curvas de aprendizaje popularmente elevadas‚Äî, que prometa a cambio una mejor perspectiva sobre
la programaci√≥n o alguno de sus aspectos tangenciales suele ganar mi inter√©s.

Quiz√° solamente es porque juego al Bloodborne y
aplico su filosof√≠a a todos los dem√°s aspectos de mi vida.
En cualquier caso, desde hace a√±os, Haskell es mi lenguaje para proyectos paralelos
y mis entornos de desarrollo y despliegue se describen v√≠a Nix.

Estas perspectivas me acompa√±an hoy d√≠a en mi trabajo diario con Rust, mi lenguaje profesional.
Creo que soy mejor programador gracias a ellas.

## Fabricando int√©rpretes

Algunos miembros del equipo de Agent Control en New Relic tenemos un club de lectura semanal.
Hace unos meses seleccionamos **Crafting Interpreters** de Robert Nystrom (puedes leerlo
gratis en [su web](<https://www.craftinginterpreters.com>)), en gran parte por ser de naturaleza m√°s
pr√°ctica que las lecturas anteriores ([**Rust for Rustaceans**](https://rust-for-rustaceans.com) de
Jon Gjenset y [**Asynchronous Programming in Rust**](https://www.packtpub.com/en-us/product/asynchronous-programming-in-rust-9781805128137) de Carl Fredrik Samson).

El libro implementa un lenguaje de programaci√≥n orientada a objetos simple llamado **Lox**. Lo hace
dos veces, con estrategias y lenguajes diferentes cada vez. Primero en Java (`jlox`) mediante
recorrido de √°rboles, luego en C (`clox`) con una m√°quina virtual de _bytecode_.

Como no quer√≠amos hacer la primera parte en Java (xd) cada uno de nosotros escogi√≥ su propio
lenguaje para la primera parte del libro.

As√≠ que ahora ya sabes a qu√© vino la introducci√≥n.

Este art√≠culo explora varios aspectos de mi implementaci√≥n de `jlox` en Haskell.

> [!tip] ¬°Puedes ver mi implementaci√≥n mientras lees!
> El c√≥digo de mi implementaci√≥n, llamada (no muy originalmente) `hox`, est√° publicado
> [en GitHub](https://github.com/DavSanchez/hox).
> Pasa toda la _suite_ de tests del repositorio oficial de Crafting Interpreters (m√°s sobre esto a
> continuaci√≥n) para `jlox`, por lo que es conforme a lo especificado en el libro.

Vamos all√°.

## Entorno reproducible con Nix

Te lo dije.

Haskell utiliza [`cabal`](https://www.haskell.org/cabal/) como gestor de proyectos. Creo que, a√∫n
con sus asperezas, es una herramienta bastante buena. Sin embargo, como ya dec√≠a al principio,
me gustan mis dependencias controladas y descritas lo m√°s completamente posible. #nix es un viejo conocido en este sitio, y por descontado la mejor herramienta a d√≠a de hoy para encargarse de eso.

As√≠ que antes de escribir mi primera l√≠nea de Haskell me asegur√© de que mi entorno de desarrollo
fuese un [_flake_](./hispanix/2024-06-25-entornos-desarrollo.md) en condiciones.
Con el _flake_ pude establecer algunos aspectos que quer√≠a desde el minuto uno:

- La versi√≥n del compilador que me interesaba
(GHC 9.12.2, para probar las extensiones de [GHC2024](https://ghc.gitlab.haskell.org/ghc/doc/users_guide/exts/control.html#extension-GHC2024))
- Un formateador "√∫nico" para todos los tipos de fuentes del proyecto con
[`treefmt`](https://github.com/numtide/treefmt).
- _Pre-commit hooks_ para todas las comprobaciones que me interesaban,
incluyendo _hooks_ custom (`hlint`, `weeder`, `doctest`, etc).
- Un _shell_ de desarrollo con todas las dependencias listas (incluyendo formateador y _hooks_).

M√°s relevante que todos estos aspectos para comprender la potencia de Nix es el c√≥mo pude utilizarlo
para ejecutar los tests del [repositorio](https://github.com/munificent/craftinginterpreters) de
Crafting Interpreters.

El c√≥digo de los bancos de tests est√° escrito en Dart 2, y desde 2023 Dart est√° en la versi√≥n 3.
Dart 3 obliga a asegurar la _Null Safety_, cosa que los tests no hab√≠an hecho porque la versi√≥n 2 no
lo requiere, as√≠ que estos tests no funcionan con la √∫ltima versi√≥n. Si fuese un programador de Dart
y tuviese instalada la versi√≥n 3 tendr√≠a que pelear con mi entorno para instalar la versi√≥n 2 y
poder ejecutar los tests sin alterar mi entorno habitual.
Tal vez alguna herramienta tipo `nvm` para Dart, `asdf` o contenedores...

Pero como uso Nix, no tengo que preocuparme de este problema en absoluto. No tengo que conocer
herramientas espec√≠ficas para el lenguaje de turno ni arriesgarme a conflictos con herramientas
instaladas globalmente (un saludo, Python).

Busco Dart en el repositorio de paquetes m√°s grande y m√°s actualizado del mundo, `nixpkgs`. Busco
la revisi√≥n de `git` de `nixpkgs` que contenga la versi√≥n de Dart que me sirve. Fijo la revisi√≥n
como entrada a mi _flake_... y listo, **Dart 2.19.6** listo para ejecutar en mi terminal. Gracias a
`direnv`, en el momento en el que salgo del directorio del proyecto el ejecutable de `dart`
desaparece (o volver√≠a a la 3.9.4 si fuese un programador de Dart con una instalaci√≥n global).

De nuevo, tirando de las funciones de Nix escribo un _script_, tambi√©n en Haskell, (**_inline!_**)
que llama a Dart como si fuese un _script_ de Bash, de forma que puedo ejecutar los tests de cada
cap√≠tulo de forma aislada y pas√°ndole mi implementaci√≥n del int√©rprete.

¬øC√≥mo? mira esto (sacado del repo de [Shh](https://github.com/luke-clifton/shh#nix)):

```nix
writers.writeHaskellBin "example" { libraries = [ haskellPackages.shh ]; } ''
  {-# LANGUAGE TemplateHaskell #-}
  import Shh

  -- Load binaries from Nix packages. The dependencies will be captured
  -- in the closure.
  loadFromBins ["${git}", "${coreutils}", "${curl}"]

  main :: IO ()
  main = do
    cat "/a/file"
    cp "/a/file" "/b/file"
''
```

F√≠jate en el comentario del centro. Hasta la versi√≥n de las `coreutils` est√° fijada por Nix.

Es un nivel de control inigualable, no vuelves a mirar (ni a ejecutar) un _script_ de Bash igual.

Por supuesto, automatizar la ejecuci√≥n de estos tests en la CI (mismas versiones de programas, etc,
etc) es trivial. Esto da confianza absoluta: puedo saber si mi implementaci√≥n actual supera los
tests oficiales y seleccionar granularmente cu√°ntos y cu√°les cap√≠tulos compruebo, independientemente del lenguaje que haya escogido.

Escrib√≠ mi CI para que cada _pull request_ al repositorio implementando un cap√≠tulo
debiese pasar todos los tests de dicho cap√≠tulo. Nada de medias tintas.

Si quieres verlo, echa un vistazo a mi [`flake.nix`](https://github.com/DavSanchez/hox/blob/1eb3f09d00cf3f1c627bfddd2963441cb2dadef2/flake.nix#L48-L127).

## Escaneando: de la mutabilidad a la recursi√≥n de cola

El primer paso de cualquier int√©rprete es el esc√°ner (o _lexer_). En Java, Nystrom usa estructuras
habituales, `while`s que avanzan sobre la cadena de caracteres de entrada, mutando el estado en cada
iteraci√≥n (`while (!isAtEnd()) { start = current; ... }`).

¬øC√≥mo te aproximas a esto en Haskell, donde casi todo es inmutable y no hay estado? No es cuesti√≥n de tirar de `map` o `foldl` sobre listas de caracteres, porque
puedes necesitar m√°s que un car√°cter cada vez para determinar ante qu√© _token_ te
encuentras. Cuando llegas a `=` necesitas m√°s para saber si est√°s ante un **signo
de igual** o ante un **operador l√≥gico** `==`.

La respuesta a que no haya estado mutable es **llevar el estado contigo en la funci√≥n**, llam√°ndola recursivamente y pasando expl√≠citamente el estado
actualizado. ¬øTe suena lo de las dependencias
expl√≠citas?.

Este es el tipo de mi funci√≥n de escaneo en Haskell.

> [!info] _First time?_
> Si nunca has visto una firma de tipos de
> Haskell, asume que todos los tipos que aparecen excepto el √∫ltimo son par√°metros de la funci√≥n,
> y el tipo que aparece al final es el tipo de retorno.
>
> Para saber m√°s, lee sobre el [_currying_](https://en.wikipedia.org/wiki/Currying).

```haskell
-- | Scans the input strings and tracks the state: the current line number and the accumulated tokens
naiveScanTokens ::
  -- | Input string (a linked list of characters, `String` == `[Char]`)
  [Char] ->
  -- | Current line number
  Int ->
  -- | Accumulated tokens (my state!)
  [TokenResult] -> 
  -- | Resulting list of tokens, a non-empty list, can contain scanning errors
  NonEmpty TokenResult
```

> [!note] ¬øNa√≠f?
> La funci√≥n se llama `naiveScanTokens` porque inicialmente explor√©, aparte de
> esta versi√≥n literalmente escrita caso a caso seg√∫n le√≠a, otra versi√≥n utilizando
> _combinadores mon√°dicos_ (ya, ya. Sigue leyendo) y la biblioteca
> [`megaparsec`](https://hackage.haskell.org/package/megaparsec).
>
> Finalmente descart√© esta segunda opci√≥n por una cuesti√≥n did√°ctica a varios niveles:
>
> - Calentamiento despu√©s de un par de meses sin tocar Haskell.
> - No usar bibliotecas externas en la implementaci√≥n.
> - La secci√≥n de _parsing_ ya me permitir√≠a usar, tambi√©n manualmente, los combinadores.
> - Introducir algunos patrones de programaci√≥n funcional y Haskell a mis compa√±eros de equipo.
> - Escribir este art√≠culo alg√∫n d√≠a.

La implementaci√≥n simplemente hace _pattern matching_ sobre los caracteres de entrada y llama a la
misma funci√≥n actualizando el estado hasta que termina. F√≠jate en c√≥mo capturo cada car√°cter y c√≥mo
vinculo el resto de la lista a `ss` con el operador `:` en los argumentos de entrada, en el lado
izquierdo de la definici√≥n, para pasarla a la nueva llamada en el lado derecho, y c√≥mo en el caso
base a√±ado del estado de entrada (el argumento `tt`) el token `EOL` a la lista de salida (por eso
nunca est√° vac√≠a) con `:|`.

```haskell
-- We start with the base case: no more characters to process, I just add the EOF token
naiveScanTokens "" l tt = validToken EOF l :| tt
naiveScanTokens ('(' : ss) l tt = naiveScanTokens ss l (validToken LEFT_PAREN l : tt)
naiveScanTokens (')' : ss) l tt = naiveScanTokens ss l (validToken RIGHT_PAREN l : tt)
-- ...
-- Operators
naiveScanTokens ('!' : '=' : ss) l tt = naiveScanTokens ss l (validToken BANG_EQUAL l : tt)
naiveScanTokens ('!' : ss) l tt = naiveScanTokens ss l (validToken BANG l : tt)
-- ...
-- Whitespaces (no newlines)
naiveScanTokens (s : ss) l tt
  | s == ' ' || s == '\r' || s == '\t' =
      naiveScanTokens ss l tt
-- ... all other cases
-- If we reach here, it means we encountered an unexpected character
naiveScanTokens (s : ss) line tt = naiveScanTokens ss line (syntaxError "Unexpected character." line [s] : tt)
```

Tienes una funci√≥n definida varias veces seg√∫n los diferentes comportamientos que quieres.
Es como sobrecargar una funci√≥n por posibles sus valores de entrada.

El c√≥digo es casi una transcripci√≥n directa de las instrucciones del libro. Iba a√±adiendo los casos
a la funci√≥n seg√∫n los le√≠a, pero sin contadores manuales ni constructos de control
de flujo `while` o `for`. Llamada Datos y estado de entrada, transformaci√≥n, y
nueva llamada con nuevos datos y estado.

Solo hay que pasar un estado inicial (la primera l√≠nea y la lista vac√≠a de resultados) junto a la
entrada, que es lo que hago con `scanTokens` (esta solo tiene que aceptar el verdadero _input_):

```haskell
scanTokens :: String -> NonEmpty TokenResult
scanTokens s = reverse (naiveScanTokens s 1 [])
```

Puedes ver la implementaci√≥n completa de `naiveScanTokens` en mi repositorio ([`Scanner/Naive.hs`](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Scanner/Naive.hs#L100))

## _Visitor_ vs _Pattern Matching_ de tipos suma

La secci√≥n [_Working with trees_](https://www.craftinginterpreters.com/representing-code.html#working-with-trees)
del cap√≠tulo 5 explora las diferencias expresivas entre el paradigma orientado a
objetos y el paradigma funcional, y c√≥mo el patr√≥n de dise√±o _visitor_ trata de
acercar el estilo funcional al orientado a objetos.

Por supuesto, al acercarme al problema desde la programaci√≥n puramente funcional,
no iba a necesitar implementar el patr√≥n _visitor_. Como el propio libro menciona:

> Each kind of expression in Lox behaves differently at runtime. That means the
> interpreter needs to select a different chunk of code to handle each expression
> type. With tokens, we can simply switch on the TokenType. But **we don‚Äôt have a
> ‚Äútype‚Äù enum** for the syntax trees, just a separate Java class for each one.

¬°Nosotros s√≠ que tenemos ese _‚Äútype‚Äù enum_!

El sistema de tipos de Haskell es una de sus grandes fortalezas. En particular,
tenemos a nuestra disposici√≥n los **ADTs** (_Algebraic Data Types_), y su forma
de tipos suma (que tambi√©n se ve en los `enum` de Rust) cubre este caso con total
naturalidad, particularmente con la sintaxis de Haskell.

La gram√°tica formal para las expresiones de Lox empieza as√≠, sacada tal cual
del libro:

```txt
expression     ‚Üí literal
               | unary
               | binary
               | grouping ;
```

Y la primera versi√≥n de mi AST de expresi√≥n expresada como un tipo en Haskell
se ve√≠a as√≠:

```haskell
-- | Represents an expression in the AST.
data Expression = Literal Literal
                | Unary UnaryOperator Expression
                | Binary BinaryOperator Expression Expression
                | Grouping Expression
```

Hay informaci√≥n que falta en cada uno de los fragmentos, claro, pero imagino que
pillas la idea üòé.

El int√©rprete se reduce a una funci√≥n que encaja cada patr√≥n, como el esc√°ner,
pero esta vez sobre los constructores de variantes del tipo.
Es una soluci√≥n m√°s concisa que evita escribir un pu√±ado de clases sin m√©todos para
implementar _visitor_, y por supuesto te ahorra escribir el generador que el autor
tiene que fabricar para agilizar el a√±adido de dichas clases (son 21).

Puedes ver mi tipo de expresi√≥n actual en [`Syntax/Expression.hs`](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Syntax/Expression.hs).

## Parseando: combinadores mon√°dicos

Esta secci√≥n tiene un poco de miga, pero creo que es la que m√°s puede mostrar la potencia de Haskell y la programaci√≥n funcional a quienes no la conocen.

La versi√≥n de Java del libro hace uso del descenso recursivo para implementar
el _parser_. Aunque la idea es similar, yo iter√© sobre el patr√≥n del esc√°ner y
apret√© un poco m√°s la tuerca usando unas estructuras bastante potentes, los combinadores mon√°dicos de _parsers_.

Veamos de qu√© van.

### ¬øQu√© es un _parser_?

Un _parser_, al fin y al cabo, no es m√°s que una **funci√≥n** que acepta una entrada
y devuelve una salida. Pero, ¬øqu√© salida?:

```haskell
parser :: Input -> ?
```

En una versi√≥n muy simple, podemos tratar de procesar toda esa entrada y devolver
nuestro resultado, que puede ser o bien tu tipo de salida (e.g. `Expression`)
o un error (quiz√° pasamos una secuencia de _tokens_ sin ning√∫n sentido).

Como estamos en el mundo puro y matem√°tico no queremos utilizar algo como
excepciones que haya que capturar luego o dejar que maten al programa, aunque
podemos. En la programaci√≥n funcional pura y est√°ticamente tipada, **los errores
tambi√©n son valores**, y el tipo de las funciones contempla el tipo de errores que
pueden emitir.

Para codificar esto en la signatura de nuestra funci√≥n hacemos uso, por supuesto,
de ADTs. Un tipo con dos variantes, el √©xito y el fallo:

```haskell
data ParseResult = Ok Output
                 | Err ParseError
```

Generalizamos a cualquier tipo y obtenemos:

```haskell
data ParseResult e a = Ok a
                     | Err e

-- Parameterizing with our types
type ParseResultAlias = ParseResult ParseError Output
```

Por supuesto, Haskell tiene ya tipos para representar esto. Tal y como Rust usa su
tipo [`Result<T, E>`](https://doc.rust-lang.org/std/result/), aqu√≠ usamos `Either e a`.

> [!info]
> Rust y Haskell usan un orden distinto para los par√°metros de tipo en `Result` y
> `Either`. Esto es porque en Haskell nos suele interesar el poder _mapear_ sobre
> el tipo que queda a la derecha.
>
> Puedes leer un poco al respecto en [este otro art√≠culo](./2024-11-13-mapeando-estructuras.md).

As√≠ que el tipo de mi funci√≥n _parser_ podr√≠a tener esta forma, generalizando sobre
el tipo de entrada (que llamamos `s`), el tipo de salida (que llamamos `a`) y el
tipo de error (que llamamos `e`):

```haskell
parser :: s -> Either e a
```

### ¬øC√≥mo se _combinan_?

Pero est√°bamos hablando de **combinadores**. ¬øQu√© es lo que combinamos? La
respuesta es piezas de _parser_ (recuerda, funciones) m√°s peque√±as.

Pero si una funci√≥n que consume toda la entrada es a su vez una combinaci√≥n de
_sub-funciones_, no tendr√≠a demasiado sentido que las funciones peque√±as
consumieran toda la entrada. De ser as√≠, solo una de ellas podr√≠a consumir la
entrada, con el resto de las _sub-funciones_ operando sobre la salida de esta, y
la combinaci√≥n de la que hablamos ser√≠a simplemente una composici√≥n directa de
funciones (e.g. `subParser3(subParser2(subParser1(input)))`).

Si contemplamos m√°s opciones que esta, entonces las _sub-funciones_, en general,
no deben consumir toda la entrada.

Entonces, ¬øc√≥mo expresamos que la entrada no ha sido consumida al completo
en nuestras funciones?

Pues as√≠:

```haskell
parser :: s -> Either e (a, s)
```

Ahora la salida del caso de √©xito de mi funci√≥n es una _**tupla**_ que contiene dos tipos, el tipo de salida **y el resto de la entrada que falta por procesar**.

> [!info]
> En mi implementaci√≥n [el tipo `Parser` no es exactamente as√≠](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Parser.hs#L25-L27) debido a su capacidad de
> [sincronizaci√≥n y recuperaci√≥n de errores](https://www.craftinginterpreters.com/parsing-expressions.html#panic-mode-error-recovery), pero no necesitamos cubrir
> ese aspecto para entender los combinadores mon√°dicos en su forma b√°sica.

Aqu√≠ est√° la forma final que tendr√≠a un _parser_ totalmente gen√©rico,
encapsulado en una estructura que engloba un √∫nico tipo de valor (la funci√≥n de
_parsing_ en s√≠) y un alias para los tipos concretos con los que vamos a trabajar:

```haskell
newtype Parser e s a = Parser
  { runParser :: s -> Either e (a, s)
  }

-- Parameterizing the input and the error, but not the output!
type TokenParser a = Parser ParseError [Token] a
```

Entonces, estos combinadores ir√°n consumiendo la entrada (`[Token]`) y devolver√°n el resultado (que puede ser o bien un tipo cualquiera `a` o un `ParseError`).

Asumiendo una secuencia de _tokens_ que √∫nicamente pueda formar una expresi√≥n
v√°lida, una combinaci√≥n de estos _parsers_ recibir√≠a la secuencia completa y
acabar√≠a con el valor `Right (Expression, [])`. El resultado con variante de la
derecha (√©xito) con mi valor de tipo `Expression` y la lista de _tokens_ consumida
al completo.

> [!tip] ¬°Solo es un esc√°ner!
> Es importante que te quedes con la idea de que esto no es muy diferente a
> lo que hice con el esc√°ner, pero subiendo un poco el nivel de abstracci√≥n.
>
> Un esc√°ner no es otra cosa que una funci√≥n `[Char] -> Either SyntaxError ([Token], [Char])`
> que consume toda la entrada de golpe, pero las llamadas recursivas intermedias s√≠
> que pasan "lo que queda de la lista de caracteres" y a su vez
> [emit√≠an o un `validToken` o un `syntaxError`](https://github.com/DavSanchez/hox/blob/d318b821e162fcd548841b0f7f5d1a9bfd380169/src/Language/Scanner/Naive.hs#L12-L18)...
> üòâ.

Llegados a este punto, ¬øC√≥mo combinamos estos _parsers_ para generar mi
`TokenParser Expression`?

### La palabra que falta

Podemos darle a nuestro tipo `Parser e s a` la capacidad de realizar cierta
operaci√≥n de encadenado con otros parsers de distintos tipos, de forma que cada
_parser_ consuma un poco de la entrada, **y luego** el siguiente _parser_ consuma
otro poco, y as√≠ hasta cuanto queramos. Nosotros determinar√≠amos cu√°ndo termina la
cadena y qu√© tipo esperamos a la salida. Idealmente, la cadena terminar√≠a cuando
no queda m√°s entrada.

Apoy√°ndonos en Rust por familiaridad, podemos buscar alguna funci√≥n que se parezca
a lo que buscamos.

Y _resulta_ (xd) que tenemos un ["**y luego**"](https://doc.rust-lang.org/std/result/enum.Result.html#method.and_then)
que se parece bastante a lo que buscamos, entre los m√©todos de `Result<T, E>`:

```rust
/// Calls `op` if the result is `Ok`, otherwise returns the `Err` value of `self`.
///
/// This function can be used for control flow based on `Result` values.
pub fn and_then<U, F>(self, op: F) -> Result<U, E>
where
    F: FnOnce(T) -> Result<U, E>,
```

`and_then` recibe como par√°metro un valor `Result<T, E>` (ese `self`) y una funci√≥n
`FnOnce(T) -> Result<U, E>` (ese `op`), devolviendo el tipo `Result<U, E>`.
Es decir, **encadena un resultado con √©xito** con una **funci√≥n que devuelve otro
resultado (con √©xito o sin √©l)**. Si el primer resultado no tuvo √©xito,
devolvemos el error directamente, sin encadenarlo con la funci√≥n `op`.

Existe un equivalente en Haskell, y el sistema de tipos nos permite expresar esta
capacidad de encadenamiento de una forma tan abstracta que, usando la terminolog√≠a
de orientaci√≥n a objetos por familiaridad, es el equivalente al m√©todo de una
interfaz. Haskell llama a estas formas de reusar c√≥digo **clases de tipo** (no
confundir con las clases en orientaci√≥n a objetos) o _typeclass_.
En Rust, el equivalente son los _traits_.

Esta clase de tipo, por fin, es la clase [`Monad`](https://hackage-content.haskell.org/package/base-4.22.0.0/docs/Prelude.html#t:Monad). De ah√≠ lo de _combinadores mon√°dicos_.

F√≠jate en el √∫nico m√©todo (o m√°s bien operador) que debe definir un tipo para
implementar esta clase `Monad`:

![Definici√≥n m√≠nima de la clase Monad](./img/hs_monad_definition.png)

> Compone dos acciones secuencialmente, pasando cualquier valor producido por la
> primera como argumento para la segunda.

Este `>>=` es un operador que une dos argumentos, un valor `m a` (la primera
acci√≥n) y una funci√≥n `(a -> m b)` (la segunda, que depende del resultado de la
primera).

Te ayudo a unir Haskell y Rust usando una suerte de √°lgebra de tipos.

- `m _` es `Result<_, E>`.
- `a` es `T`, por tanto `m a` es `Result<T, E>`.
- `b` es `U`, por tanto `m b` es `Result<U, E>`.
- Por tanto, `a -> m b` es `FnOnce(T) -> Result<U, E>`
  - Obviemos _ownership_, sem√°nticas de movimiento de valores, etc, y aceptemos ese `FnOnce` como _funci√≥n en general_ por ahora.

```haskell
-- Haskell       -- Rust
(>>=)            -- fn and_then(
  :: m a         --   self: Result<T, E>,
  -> (a -> m b)  --   op: FnOnce(T) -> Result<U ,E>
  -> m b         -- ) -> Result<U, E>
```

**¬°Son lo mismo!** La diferencia es que Rust no puede expresar ese `and_then` para
ciertos grupos de tipos como parte de un _trait_, como tambi√©n exploramos en mi
[anterior art√≠culo](./2024-11-13-mapeando-estructuras.md).

## 5. El Gran Reto: "Resolution Distance" y √Årboles que Crecen

Uno de los desaf√≠os m√°s interesantes fue el cap√≠tulo de **Resoluci√≥n de Variables**. En Lox, para manejar cierres (closures) correctamente, necesitamos saber a cu√°ntos "saltos" (scopes) de distancia est√° definida una variable.

### La soluci√≥n Java: Side-Tables

En Java, el Resolver recorre el √°rbol y guarda esta informaci√≥n en un `Map<Expr, Integer>` separado. El AST no cambia. Esto funciona en Java porque cada objeto `Expr` tiene identidad (direcci√≥n de memoria) y puede usarse como clave en un mapa.

### El problema en Haskell

En Haskell, los valores son inmutables y estructurales. `Variable "a"` es id√©ntico a otro `Variable "a"`. No tienen "identidad" por defecto. Para usar un `Map`, necesitar√≠a adjuntar IDs √∫nicos a cada nodo o usar trucos inseguros.

### La soluci√≥n Haskell: Tipos Dependientes de la Fase

En lugar de una tabla lateral, decid√≠ que la informaci√≥n de resoluci√≥n deb√≠a vivir **dentro** del propio AST. Pero el Parser no conoce esa informaci√≥n todav√≠a. ¬øC√≥mo definimos un tipo que a veces tiene resoluci√≥n y a veces no?

Us√© una t√©cnica inspirada en el paper ["Trees That Grow"](https://www.microsoft.com/en-us/research/publication/trees-that-grow/), utilizando **Data Kinds** y **Type Families**:

```haskell
-- Definimos las fases del compilador
data Phase = Unresolved | Resolved

-- Una familia de tipos que cambia seg√∫n la fase
type family ResolutionInfo (p :: Phase) :: Type where
  ResolutionInfo 'Unresolved = NotResolved  -- Un tipo vac√≠o
  ResolutionInfo 'Resolved = Resolution     -- La distancia calculada (Global | Local Int)

-- El AST usa esta familia de tipos
data Expression (p :: Phase)
  = VariableExpr
      Int                -- L√≠nea
      String             -- Nombre
      (ResolutionInfo p) -- ¬°Cambia seg√∫n la fase!
  -- ...
```

Esto es **Type-Driven Development** puro.

1. El `Parser` produce un `Program 'Unresolved`.
2. El `Resolver` toma un `Program 'Unresolved` y devuelve un `Program 'Resolved`.
3. El `Interpreter` **solo** acepta `Program 'Resolved`.

```haskell
-- Interpreter.hs
programInterpreter :: Program 'Unresolved -> Interpreter ()
programInterpreter prog = do
  let (resolvedProg, errors) = runResolver (programResolver prog)
  if null errors
    then interpretProgram resolvedProg -- Aqu√≠ GHC sabe que es seguro
    else throwError (Resolve errors)
```

Si intento ejecutar un programa sin resolver, ¬°el c√≥digo ni siquiera compila! Es una garant√≠a de seguridad que Java no puede ofrecer tan f√°cilmente.

## 6. Arqueolog√≠a de Tipos: El Paso por GADTs

En las primeras etapas de este proyecto, experiment√© con **GADTs (Generalized Algebraic Data Types)** para representar el AST. La idea era ambiciosa: codificar en el sistema de tipos que ciertas operaciones solo eran v√°lidas en fases espec√≠ficas. Por ejemplo, que el constructor `Variable` solo pudiera contener una `Resolution` si el tipo de la expresi√≥n era `Resolved`.

```haskell
data Expression p where
  VariableExpr :: String -> Resolution -> Expression 'Resolved
  -- ...
```

Sin embargo, pronto me top√© con la rigidez de este enfoque para un AST tan grande. Aunque extremadamente seguro, los GADTs pueden complicar tareas como el derivado autom√°tico de instancias (`Show`, `Eq`) y requieren un "ceremonial" de tipos que a veces oscurece la l√≥gica del compilador.

Finalmente, pivot√© hacia el patr√≥n **"Trees That Grow"** (usando `DataKinds` y `TypeFamilies`) que mencion√© antes. Es una soluci√≥n m√°s "haskelliana" para este problema espec√≠fico: permite extender el AST con metadatos seg√∫n la fase sin perder la ergonom√≠a de los ADTs tradicionales.

## 7. Gestionando el Estado: Capacidades y MTL

En Java, el int√©rprete es una clase con campos mutables (`environment`). En Haskell, la mutabilidad y los efectos deben ser expl√≠citos. Mi implementaci√≥n utiliza la librer√≠a [**MTL (Monad Transformer Library)**](https://hackage.haskell.org/package/mtl) para construir una "cebolla" de efectos:

```haskell
newtype InterpreterT m a = Interpreter
  { runInterpreterT :: StateT (ProgramState Value) (ExceptT InterpreterError m) a
  }
```

Visualmente, la pila de efectos se ve as√≠:

```mermaid
graph BT
    IO["IO (Base: Efectos de consola)"]
    ExceptT["ExceptT (Manejo de Errores)"] -->|envuelve| IO
    StateT["StateT (Entorno y Variables)"] -->|envuelve| ExceptT
    InterpreterT["InterpreterT (Newtype Wrapper)"] -->|envuelve| StateT
```

Lo interesante aqu√≠ no es solo la pila de m√≥nadas, sino c√≥mo se utiliza. Siguiendo las mejores pr√°cticas, la mayor√≠a de las funciones del int√©rprete no conocen esta pila concreta. En su lugar, utilizan **restricciones de clases de tipos** (capabilities):

```haskell
evaluateExpr :: (MonadState (ProgramState Value) m, MonadError InterpreterError m) => Expression 'Resolved -> m Value
```

Esta firma dice: "esta funci√≥n funciona en cualquier m√≥nada `m` que tenga un estado de tipo `ProgramState` y pueda lanzar errores de tipo `InterpreterError`". Esto desacopla la l√≥gica de la implementaci√≥n concreta, facilitando los tests y permitiendo reutilizar funciones en diferentes contextos (como el Resolver, que comparte algunas de estas capacidades pero no todas).

Un detalle elegante es c√≥mo se manejan los scopes. En Java se usa `try-finally` para asegurar que, al salir de un bloque, el entorno anterior se restaura. En Haskell, uso `catchError` junto con las primitivas de estado:

```haskell
executeBlock decls = do
  modify pushScope        -- Entrar en nuevo scope
  -- Si 'go decls' falla, ejecutamos el handler que hace popScope y relanza el error
  r <- catchError (go decls) (\e -> modify popScope >> throwError e)
  modify popScope         -- Salir del scope (caso exitoso)
  pure r
```

## 8. Errores como Valores

Una de las diferencias filos√≥ficas m√°s grandes es el manejo de errores. En lugar de que el flujo de control salte m√°gicamente (Exceptions), los errores son valores (`Left InterpreterError`) que se propagan.

Aunque `ExceptT` hace que parezca imperativo (puedes hacer short-circuiting), te obliga a ser consciente de en qu√© partes del c√≥digo pueden fallar las cosas. No hay "Unchecked Exceptions" aqu√≠.

## 9. Az√∫car Sint√°ctico: Pattern Synonyms

En mi implementaci√≥n del flujo de control (bucles, retornos de funci√≥n), me encontr√© con un patr√≥n com√∫n. Necesitaba un tipo que representara "seguir ejecutando" o "detenerse y devolver un valor".

Podr√≠a haber usado `Either b c`, pero quer√≠a algo m√°s descriptivo. Defin√≠ `ControlFlow`:

```haskell
data ControlFlow b c
  = Break b     -- Detener el flujo con un valor (ej. return)
  | Continue c  -- Seguir ejecutando
```

Sin embargo, leer `Break` cuando estamos implementando una sentencia `return` es cognitivamente disonante. Aqu√≠ es donde los **Pattern Synonyms** de Haskell brillan. Me permiten crear un "constructor virtual" que aliasa a uno existente:

```haskell
{-# LANGUAGE PatternSynonyms #-}

pattern Return :: b -> ControlFlow b c
pattern Return x = Break x
```

Ahora, mi c√≥digo del int√©rprete puede usar `Return` como si fuera un constructor real, haciendo que la intenci√≥n sea cristalina:

```haskell
runFunctionBody (d : ds) =
  interpretDeclF d >>= \case
    Return v -> pure v        -- ¬°Se lee como ingl√©s!
    Continue () -> runFunctionBody ds
```

Es un peque√±o detalle, pero demuestra c√≥mo Haskell te permite modelar el dominio de tu problema no solo en los tipos, sino tambi√©n en la sintaxis misma. ¬øEs sobre-ingenier√≠a para un int√©rprete de juguete? Quiz√°s. Pero parte de la diversi√≥n de usar un lenguaje expresivo es ver hasta d√≥nde puedes doblarlo para que se ajuste a tus conceptos mentales, y no al rev√©s.

## 10. Propiedades, no solo Tests

Adem√°s de la suite oficial de tests de Lox (que son casos de ejemplo espec√≠ficos), aprovech√© el ecosistema de Haskell para a√±adir **Property-Based Testing** con `QuickCheck`.

En lugar de escribir `assert(scan("((") == [LEFT_PAREN, LEFT_PAREN])`, defino propiedades universales que mi c√≥digo debe cumplir para _cualquier_ cadena de entrada generada aleatoriamente. Por ejemplo, mi esc√°ner debe cumplir siempre que:

1. La lista de tokens resultante nunca puede ser m√°s larga que la cadena de entrada (m√°s uno por el EOF).
2. El escaneo siempre termina en un token `EOF` o en un error de "String no terminado".

```haskell
-- test/Scanner/Props.hs
scannerProperties :: TestTree
scannerProperties = 
  testGroup "Scanner Property Tests"
    [ testProperty "Always ends in EOF or an unterminated string error" eofOrUnterminatedString
    , testProperty "Less or equal tokens than input length" lessOrEqualTokensThanInputLength
    ]
```

Es una forma de testear que encuentra casos borde (como cadenas vac√≠as, o llenas de caracteres nulos) que a un humano dif√≠cilmente se le ocurrir√≠an escribir manualmente.

## 11. Midiendo el Rendimiento: Benchmarks en CI

No se puede mejorar lo que no se mide. Para asegurarme de que mis refactorizaciones "idiom√°ticas" no destrozaran el rendimiento del int√©rprete, integr√© una suite de **benchmarks** utilizando `tasty-bench`.

Eleg√≠ `tasty-bench` sobre el cl√°sico `criterion` por su ligereza y su integraci√≥n natural con el resto de mi infraestructura de tests. El benchmark can√≥nico para int√©rpretes es la funci√≥n de Fibonacci recursiva, que estresa la resoluci√≥n de variables, las llamadas a funci√≥n y las operaciones aritm√©ticas b√°sicas.

```haskell
-- bench/Main.hs
main :: IO ()
main = defaultMain
  [ bgroup "Interpreter"
      [ bench "fib(20)" $ whnfIO $ runInterpreter $ buildTreeWalkInterpreter $ Right $ tokens 20
      , bench "fib(25)" $ whnfIO $ runInterpreter $ buildTreeWalkInterpreter $ Right $ tokens 25
      ]
  ]
```

Pero ejecutar benchmarks en mi m√°quina no es suficiente. Quer√≠a visualizar la tendencia hist√≥rica. Para ello, configur√© un workflow de **GitHub Actions** que:

1. Ejecuta los benchmarks en Linux (x64 y ARM) y macOS.
2. Convierte los resultados a JSON usando un peque√±o script de Python.
3. Utiliza la acci√≥n `benchmark-action/github-action-benchmark` para generar gr√°ficas de evoluci√≥n y alertar si un commit introduce una regresi√≥n de rendimiento superior al 150%.

Esto convierte el rendimiento en una propiedad observable del proyecto, tan importante como la correcci√≥n del c√≥digo.

## Conclusi√≥n

Implementar Lox en Haskell ha sido un ejercicio de traducci√≥n cultural.

- Donde OOP ve **Comportamiento encapsulado con Datos**, FP ve **Datos puros y Funciones transformadoras**.
- Donde OOP usa **Identidad de Objetos**, FP usa **Tipos Estructurales**.
- Donde OOP usa **Excepciones y Mutabilidad**, FP usa **M√≥nadas y Transformadores**.

Arquitecturas como la **Resoluci√≥n basada en Tipos** me han mostrado un poder de expresividad y seguridad que dif√≠cilmente quiero abandonar para volver al mundo de `void foo()`. Haskell es una herramienta inmensamente poderosa que no solo resuelve problemas, sino que afila la mente del programador en el proceso.

El c√≥digo completo est√° disponible en el repositorio.

¬øEl siguiente paso? El libro _Crafting Interpreters_ tiene una segunda parte: una m√°quina virtual de bytecode escrita en C (`clox`). Para cerrar el c√≠rculo, planeo implementar esta segunda parte en **Rust**, aplicando (o ignorando deliberadamente) las lecciones funcionales aprendidas aqu√≠ en un entorno de sistemas de bajo nivel.

¬°Feliz hacking funcional!
