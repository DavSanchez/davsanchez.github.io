---
title: "Entornos de desarrollo reproducibles con Nix"
date: 2024-06-26
tags:
  - nix
  - tech
  - ðŸ‡ªðŸ‡¸
---
<!-- LTeX: language=es -->

![Imagen de cabecera del repositorio de Nix en GitHub](./nix.png)

Tras la introducciÃ³n a Nix que vimos en el [artÃ­culo anterior](./2024-06-16-conociendo-nix.md), continuamos con uno de los casos de uso mÃ¡s potentes de esta herramienta.

> [!warning]
> De forma similar al [artÃ­culo anterior](./2024-06-16-conociendo-nix.md), a partir de aquÃ­ asumiremos que has instalado Nix y activado dos configuraciones opcionales que facilitan mucho la experiencia de usuario con Nix. Si usaste el instalador oficial, puedes activarlas individualmente para cada comando del terminal usando `nix --extra-experimental-features "nix-command flakes"`, o activarlas globalmente aÃ±adiendo lo siguiente a `~/.config/nix/nix.conf` o `/etc/nix/nix.conf`:
>
> > ```conf
> > experimental-features = nix-command flakes
> > ```
<!--  -->
> [!info]
> A no ser que se indique lo contrario, los ejemplos a continuaciÃ³n estÃ¡n hechos en un sistema **macOS** utilizando **zsh** como shell por defecto.

Imagina que tienes un proyecto de software en el que estÃ¡s trabajando, quizÃ¡ en las etapas iniciales. No tienes interÃ©s en empaquetar el producto en Nixpkgs ni nada parecido, porque quizÃ¡ aÃºn no tienes nada que lanzar aÃºn, pero sÃ­ que te gustarÃ­a:

- Asegurarte de que tienes todas las herramientas que tienes para desarrollar este proyecto, como por ejemplo:
  - La versiÃ³n *nightly* de Rust con varios objetivos de compilaciÃ³n diferentes al por defecto, como WASM u otros.
  - Una versiÃ³n de Go especÃ­fica, diferente a una que puedas tener instalada globalmente vÃ­a otras herramientas como Homebrew.
  - La CLI de AWS/Ansible/Terraform.
  - Alguna utilidad de Python que normalmente instalarÃ­as con `pip`, como `jmespath`.
  - Variables de entorno especÃ­ficas fijadas a un valor particular (`AWS_PROFILE` o alguna configuraciÃ³n de git).
- Asegurarte de que tus *pipelines* de CI/CD tienen estas mismas herramientas, en las mismas versiones, para que tu equipo pueda ejecutar localmente las mismas acciones que la *pipeline* CI/CD y obtener el mismo resultado.
- Forzar un estilo particular de mensajes en los *commits*, o ejecutar las mismas herramientas (formateadores, *linters* o una herramienta propia) con la misma configuraciÃ³n cada vez que un miembro del equipo crea una revisiÃ³n o *commit*.
- Prevenir que las herramientas que has instalado para tu proyecto entren en conflicto con otros proyectos de tu equipo, sin por ello tener que utilizar contenedores:
  - El Proyecto A usa algÃºna funcionalidad solo disponible en Go 1.20 y posterior.
  - El Proyecto B no estÃ¡ listo para actualizar mÃ¡s allÃ¡ de Go 1.19 por ciertos problemas con CGO y `libresolv`.
- Hacer que cada nueva incorporaciÃ³n al equipo estÃ© listo para usar todas estas herramientas y convenciones fÃ¡cilmente.

Â¿CÃ³mo podrÃ­as lograr todo esto sin recurrir a contenedores, Docker y demÃ¡s?

AverigÃ¼Ã©moslo.

Recuerda que una de las posibles [*salidas*](./2024-06-16-conociendo-nix.md) que pueden definirse en un *Nix flake* son **entornos de desarrollo**. Esto significa que, usando Nix, puedes ir al repositorio de tu proyecto, "convertirlo" en un *flake* (tan solo aÃ±adiendo un fichero `flake.nix` en su raÃ­z), y con solo algunas instrucciones en el lenguaje Nix puedes configurar todo lo que mencionamos en la lista. AÃ±ade el fichero a tu control de versiones y todos los miembros de tu equipo, las *pipelines* CI/CD, todo el mundo podrÃ¡ comenzar a usarlo.

No mÃ¡s "pues en mi mÃ¡quina funciona" o instalar la misma herramienta con `apt-get` en Ubuntu, `Dockerfile` con `FROM ubuntu:latest` en macOS, una GitHub Action en tu CI/CD y golpearte la cabeza contra la pared cuando alguno de los tres entornos falla.

## Nuestro primer *flake*

> [!tip]
> Esta seguramente sea tu primera exposiciÃ³n al lenguaje de programaciÃ³n Nix. No entraremos en demasiado detalle por ahora, confiando en que no serÃ¡ difÃ­cil intuir lo que ocurre en los comienzos, pero si necesitas recursos para profundizar, echa un vistazo a [esta web interactiva](https://zaynetro.com/explainix) o al [tutorial oficial](https://nix.dev/tutorials/nix-language.html). Â¡O no dudes en preguntarme!

### Estructura bÃ¡sica

Un fichero `flake.nix` contiene lo que en lenguaje Nix se denomina un **conjunto de atributos** (*attribute set* o *attrset* para abreviar), algo muy similar a un objeto JSON:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {}; # Omitimos los contenidos de momento

  outputs = {}; # Esto que lees es un comentario
}
```

Recordemos que Nix hace que nuestras *builds* sean declarativas y **reproducibles**. Cualquier salida que definamos vendrÃ¡ determinada por las entradas que definamos, y por nada mÃ¡s. Veamos, entonces, cÃ³mo se definen estos *inputs* (entradas) y *outputs* (salidas).

#### Entradas

Para declarar paquetes como dependencias, estos paquetes tienen que venir de alguna parte. Â¿De dÃ³nde vienen, pues? Â¿QuiÃ©n los define?

La respuesta la tenemos en el artÃ­culo anterior, en el que mencionÃ© que Nixpkgs es un repositorio de mÃ¡s de 100000 paquetes instalables. Declaremos entonces Nixpkgs, que tambiÃ©n es un *flake*, como entrada:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    # Puedes darle un nombre cualquiera a tu input, lo que determina quÃ© es es la URL.
    nixpkgs = {
      # Consulta el artÃ­culo anterior para recordar quÃ© significaba esta referencia.
      url = "github:NixOS/nixpkgs/nixos-23.11";
    };
  };

  outputs = {};
}
```

Esta sintaxis es Ãºtil si queremos personalizar cada entrada, pero no cubriremos esto en este artÃ­culo. Si solo queremos aÃ±adir entradas de una forma bÃ¡sica, solo necesitando su URL, podemos hacer esto en su lugar:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
  inputs.git-hooks.url = "github:cachix/git-hooks.nix";

  outputs = {};
}
```

O agrupar las entradas de esta otra forma:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = {};
}
```

#### Salidas

AquÃ­ es donde se pone interesante. Las salidas definen lo que ofrece nuestro *flake*, y deberÃ­an utilizar de alguna forma las entradas que hemos definido anteriormente. Como ya hemos comentado, Nix recibe mucha influencia de la programaciÃ³n funcional, por tanto, **la salida estÃ¡ definida como una funciÃ³n que recibe las entradas como parÃ¡metro, y devuelve un nuevo conjunto de atributos**. Las funciones en Nix se definen con la forma `myFunction = arg1: arg2: ... argN: returned_value` (sÃ­, esto es [currying](https://en.wikipedia.org/wiki/Currying)). Por tanto, para nuestra salida:

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = {
    nixpkgs,
    git-hooks
  }: {}; # Esto Ãºltimo es el conjunto de atributos que devuelve la funciÃ³n. VacÃ­o de momento.
}
```

SupÃ³n que hemos definido muchas entradas, pero solo un pequeÃ±o subconjunto de ellas es importante y queremos que exista una referencia fÃ¡cil a ellas en todo momento, mientras que las demÃ¡s solo se referenciarÃ¡n ocasionalmente. Podemos des-estructurar el conjunto de atributos de la siguiente forma (que reconocerÃ¡n los programadores de Haskell o Rust):

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = inputs @ { # Vinculamos todas las entradas a "inputs".
    nixpkgs,           # Queremos referencia fÃ¡cil a nixpkgs
    ...                # Omitimos lo demÃ¡s
  }: {
                       # Ahora podemos usar las referencias aquÃ­.
                       # Ya sea refiriÃ©ndose a "nixpkgs" directamente o,
                       # si queremos acceder a git-hooks, usando
                       # "inputs.git-hooks".
  };
}
```

##### AÃ±adiendo dependencias

Ya tenemos la forma bÃ¡sica y queremos tener algo disponible rÃ¡pidamente. Definamos nuestro entorno de desarrollo, una *shell* personalizada con el atributo [**devShell**](https://nixos.org/manual/nixpkgs/stable/#sec-pkgs-mkShell):

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
  };

  outputs = {
    nixpkgs,
    ...
  }: let
    macM1Packages = nixpkgs.legacyPackages.aarch64-darwin;
    linuxAMD64Packages = nixpkgs.legacyPackages.x86_64-linux;
  in {
    devShells.aarch64-darwin.default = macM1Packages.mkShell {
      packages = [
        # Al momento de escribir esto, la versiÃ³n de
        # Go_1_19 en nixpkgs/nixos-23.11 es 1.19.13
        macM1Packages.go_1_19
      ];
    };

    devShells.x86_64-linux.default = linuxAMD64Packages.mkShell {
      # Nada por ahora
    };
  };
}
```

###### Â¿Es obligatorio definir *devShells* y usar referencias a los conjuntos de paquetes para cada sistema por separado?

**La respuesta rÃ¡pida es *Â¡No!***.

La respuesta larga es que Nix sirve para construir paquetes, una herramienta o sistema de *builds*, y que por tanto es necesario poder ser consciente del sistema o *target* para el que estamos construyendo un paquete, *shell* o salida en general, pues en general un software no puede ser compilado sin mÃ¡s para todos los posibles sistemas que existen.

Dicho esto, para el caso que nos ocupa en este tutorial, combinaciones de sistema operativo y arquitectura bastante extendidas y de propÃ³sito general, sÃ­ que podemos asumir que cierto software pueda compilar para ellas sin demasiado problema. Otros desarrolladores han hecho el trabajo duro por nosotros y ofrecen algunas funciones y sistemas para minimizar la repeticiÃ³n, a cambio de un poquito mÃ¡s de cÃ³digo Nix. Una de estas utilidades es [`flake-utils`](https://github.com/numtide/flake-utils). Para utilizarla imagino que ya intuirÃ¡s lo que necesitamos: Definirla como entrada.

```nix
{
  description = "Nix flake para mi proyecto en Go";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-23.11";
    git-hooks.url = "github:cachix/git-hooks.nix";
    flake-utils.url = "github:numtide/flake-utils"; # Nuevo input!
  };

  outputs = {
    nixpkgs,
    flake-utils,
    ...
  }: flake-utils.lib.eachDefaultSystem (system: let
    pkgs = nixpkgs.legacyPackages.${system};
  in {
    devShells.default = pkgs.mkShell {
      packages = with pkgs; [
        go_1_19
        terraform
      ];
    };
  });
}
```

Este `eachDefaultSystem` que hemos utilizado es una funciÃ³n que recibe otra funciÃ³n como parÃ¡metro (ya hablÃ© de la programaciÃ³n funcional), y esta funciÃ³n a su vez recibirÃ­a un parÃ¡metro que llamamos `system`, que utilizamos en el cuerpo de la funciÃ³n para definir el conjunto de paquetes que definirÃ¡ nuestro *devShell*. Â¿QuÃ© argumento de `system` usa esa funciÃ³n? De eso se encarga `eachDefaultSystem`, que pasa una lista con los siguientes valores:

- `x86_64-linux`
- `aarch64-linux`
- `x86_64-darwin`
- `aarch64-darwin`

> [!tip]
> Para comenzar de forma simple he sugerido utilizar `flake-utils`, pero para algo mÃ¡s avanzado y modular, que uso actualmente en mis *flakes*, recomiendo [`flake-parts`](https://flake.parts).

Vamos a ver cuÃ¡les son las salidas de nuestro *flake* ejecutando `nix flake show` en el directorio raÃ­z de tu *flake* (o, ya sabes, `nix flake show "path:/ruta/a/tu/flake"`). Nix tardarÃ¡ un tiempo en evaluar el cÃ³digo Nix de tu *flake*, descargar las entradas y evaluar las salidas, pero finalmente se mostrarÃ¡ algo parecido a esto:

```console
$ nix flake show
warning: creating lock file '/Users/david/random/flake.lock'
path:/Users/david/random?lastModified=1695943648&narHash=sha256-CbZ16gnffi0%2B9ig270ifpbHYOKS4CreKnH7GlPi
â””â”€â”€â”€devShells
    â”œâ”€â”€â”€aarch64-darwin
    â”‚   â””â”€â”€â”€default: development environment 'nix-shell'
    â”œâ”€â”€â”€aarch64-linux
    â”‚   â””â”€â”€â”€default omitted (use '--all-systems' to show)
    â”œâ”€â”€â”€x86_64-darwin
    â”‚   â””â”€â”€â”€default omitted (use '--all-systems' to show)
    â””â”€â”€â”€x86_64-linux
        â””â”€â”€â”€default omitted (use '--all-systems' to show)
```

El fichero `flake.lock` es un *lockfile*, que tiene un propÃ³sito similar a los probablemente mÃ¡s conocidos `package-lock.json` para proyectos del ecosistema JavaScript, `Gemfile.lock` para Ruby, `Cargo.lock` para Rust, etc. ContendrÃ¡ los *hashes* de todas las entradas de nuestro *flake*, asegurando la reproducibilidad e integridad de nuestras entradas.

#### Entrando en la *shell*

Entonces, Â¿quÃ© falta? Â¿CÃ³mo accedemos a la versiÃ³n de Go que hemos declarado en este entorno de desarrollo? Podemos hacerlo con `nix develop`:

```console
$ which go
/opt/homebrew/opt/go # Ejemplo de una instalaciÃ³n de Go existente

$ go version
go version go1.20.8 darwin/arm64

$ nix develop
# Se tomarÃ¡ su tiempo, pero despuÃ©s...

$ which go
/nix/store/3yndvq32rxh6h9bqjd6n20npk2ix0ah2-go-1.19.13/bin/go

$ go version
go version go1.19.13 darwin/arm64
```

Â¡Listo! **Como si fuera un `virtualenv` de Python, pero para cualquier lenguaje y mÃ¡s**, usar `nix develop` nos introduce en una nueva *shell* con los paquetes que definimos disponibles en el `$PATH`. Â¡De forma nativa! Â¡Sin contenedores, volÃºmenes, etc!

Ahora escribe algo de cÃ³digo, compÃ­lalo[^go-compile], y ya hablaremos de cÃ³mo empaquetar tu programa de Go con Nix en un futuro artÃ­culo.

Para salir de esta *shell* y volver al estado anterior a utilizar `nix develop`, simplemente ejecuta `exit` o usa `Ctrl + D`.

Por supuesto, podrÃ¡s intuir que esto no es lo Ãºnico que se puede hacer en cuanto a definir *shells* o empaquetar software. Puedes hacer chequeos automatizados, usar *shell hooks* para ejecutar acciones al entrar en la *shell*, configurar *pre-commit hooks* para personalizar tu trabajo con `git`, referenciar otros ficheros dentro del *flake* (como una configuraciÃ³n en formato YAML) con `import`, definir *bundlers* y empaquetar el software para sistemas que no usen Nix... Â¡Y mucho mÃ¡s!

> [!tip]
> Tampoco es completamente necesario entrar a picar cÃ³digo Nix y definir todo esto a mano. Aunque lo recomiendo para que saber mÃ¡s o menos lo que ocurre, hay muchas iniciativas para abstraer muchos de los mecanismos internos y comenzar a trabajar mÃ¡s rÃ¡pidamente.
>
> El que puedo recomendar es [`devenv.sh`](https://devenv.sh), cuyo autor es un contribuidor principal del ecosistema (tambiÃ©n autor de [`cachix`](https://www.cachix.org), `git-hooks.nix` y algunas GitHub Actions para instalar Nix en los *runners*). `devenv.sh` tambiÃ©n se configura con el lenguaje Nix, pero de forma bastante sencilla.
>
> TambiÃ©n hay alternativas que utilizan YAML o JSON, como [Flox](https://flox.dev) o [DevBox](https://www.jetify.com/devbox), respectivamente. Estos Ãºltimos apenas los conozco, y aunque pueden ofrecer algunas comodidades, en mi opiniÃ³n parecen abstraer demasiado lo que ocurre y podrÃ­an no cubrir todos los casos de uso particulares.
>
> Como ves, el ecosistema es [muy amplio](https://nix-community.github.io/awesome-nix/).

Probablemente tambiÃ©n tengas muchas preguntas. Trato de responder a algunas de ellas en el apartado siguiente, enlazando a recursos relevantes, aunque puedo desarrollar estos temas en futuros artÃ­culos o si me preguntas directamente.

## Algunas preguntas y respuestas rÃ¡pidas

### Â¿CÃ³mo sÃ© si un paquete que necesito estÃ¡ disponible en Nixpkgs?

Puedes buscar [aquÃ­](https://search.nixos.org/packages). Si utilizas DuckDuckGo, puedes acceder a esta bÃºsqueda rÃ¡pidamente con `!nixpkgs`. Por ejemplo, usa `!nixpkgs python` para ver quÃ© paquetes relacionados con Python hay en Nixpkgs.

### Trabajar con *devShells* descarga cosas en mi sistema. Â¿CÃ³mo limpio lo que ya no necesito?

Aunque puede dar la impresiÃ³n de que no *instalas* nada de forma permanente en tu mÃ¡quina cuando configuras todos estos entornos de desarrollo y descargas paquetes con Nix, obviamente todos estos recursos estÃ¡n en tu sistema y ocupan espacio.

Nix usa un recolector de basura para eliminar contenido de la Nix Store que no utilizas. Este recolector puede configurarse para ejecutar regularmente, o puedes llamarlo directamente con `nix store gc` (no necesitas estar dentro de un *flake* o un *devShell* para ello).

Como intuirÃ¡s, hay mucho mÃ¡s detrÃ¡s de esta operaciÃ³n. Â¿CÃ³mo puede Nix identificar lo que estÃ¡ en uso y lo que no? Â¿EstÃ¡ esto relacionado con los enlaces simbÃ³licos que se crearon cuando ejecutamos `nix build` en [el tutorial anterior](./2024-06-16-conociendo-nix.md)?

Puedes leer mÃ¡s [aquÃ­](https://nixos.org/guides/nix-pills/11-garbage-collector.html) y [aquÃ­](https://nixos.wiki/wiki/Storage_optimization).

### Â¿Tengo que escribir siempre los *flakes* desde cero?

Existe un mecanismo para descargar plantillas, que tambiÃ©n pueden exponerse como salidas de un *flake*. Por ejemplo, yo mantengo un *flake* con [algunas de ellas](https://github.com/DavSanchez/nix-dotfiles/tree/master/templates) (aunque no las he actualizado para que usen `flake-parts` aÃºn).

- Para proyectos de Rust, con algunas utilidades para configurar las *toolchains* (utilizando [fenix]) y *git hooks*. Puedes descargarla con `nix flake init -t "github:DavSanchez/nix-dotfiles#rust_fenix_precommit"`.
- Para proyectos de Go, con *git hooks*, usa `nix flake init -t "github:DavSanchez/nix-dotfiles#go_precommit"`
- Para proyectos con Haskell y *git hooks*, usa `nix flake init -t "github:DavSanchez/nix-dotfiles#haskell_precommit"`.

### Â¿CÃ³mo configuro los *git hooks*?

Si leÃ­ste el apartado anterior, verÃ¡s que menciono repetidamente los *git hooks*. En mi opiniÃ³n son una herramienta muy Ãºtil para asegurarte de que tu cÃ³digo cumple ciertos estÃ¡ndares antes de realizar un *commit* (Â¿pasa los tests unitarios? Â¿EstÃ¡ formateado?) o para usar algÃºn formato en tus mensajes de *commit*, como [*conventional commits*](https://www.conventionalcommits.org/en/v1.0.0/).

Mis plantillas definen como entrada [`cachix/git-hooks.nix`](https://github.com/cachix/git-hooks.nix) para definir estos *hooks* con Nix y cargarlos directamente en tu *devShell* con *shellHook*. Echa un vistazo al repositorio de `git-hooks.nix` para ver quÃ© *hooks* estÃ¡n disponibles y cÃ³mo crear los tuyos propios.

### Â¿CÃ³mo personalizo mis *devShells*?

Esto depende de tus preferencias personales. `nix develop` utiliza Bash por defecto. Yo utilizo [`zsh-nix-shell`](https://github.com/chisui/zsh-nix-shell), un plugin que permite usar ZSH como *devShell*, y tambiÃ©n [`starship`](https://starship.rs/) para personalizar el *prompt*.

### Â¿Tengo que ejecutar `nix develop` cada vez que quiero entrar en la *devShell*?

Puedes automatizar esto gracias a [`direnv`](https://direnv.net). Si echaste un vistazo a mis plantillas verÃ¡s que incluyen un fichero `.envrc`. Este fichero ayuda a conseguir este comportamiento. Si me muevo dentro de un directorio que es un *flake* con `cd`, la *devShell* se activa automÃ¡ticamente. Si hago `cd` y salgo de la *shell*, se desactiva.

Hay un artÃ­culo en el [blog de Determinate Systems](https://determinate.systems/posts/nix-direnv) con mÃ¡s detalles.

### Â¿QuÃ© mÃ¡s se puede hacer?

Â¿PodrÃ­a usar Nix en mis lÃ­neas de CI/CD con [GitHub Actions](https://nix.dev/tutorials/nixos/continuous-integration-github-actions)? Â¿Puedo configurar todo mi [sistema](https://github.com/DavSanchez/nix-dotfiles/) o mi [usuario](https://github.com/nix-community/home-manager) de forma declarativa con Nix? Â¿Aunque estÃ© en [macOS](https://github.com/LnL7/nix-darwin)? Â¿QuÃ© hay de [NixOS](https://search.nixos.org/options?query=options)?

Probablemente exploremos todas estas cosas en futuros artÃ­culos. De momento ahÃ­ tienes algunos enlaces a documentaciÃ³n y plantillas para ir abriendo boca.

Â¡Nos vemos en el siguiente!

[^go-compile]: Para los lenguajes compilados para Linux y que usen enlazado dinÃ¡mico (como Go al habilitar CGO) hay ciertos detalles de distribuciÃ³n que no hemos cubierto en este artÃ­culo y que te puedes encontrar si avanzas mÃ¡s por tu cuenta. Si las rutas de todas las dependencias estÃ¡n en la Nix Store, dÃ³nde espera un binario con *dynamic linking*, generado con Nix, encontrar al *dynamic loader*? Â¿Sigue siendo en rutas como `/lib64/ld-linux-x86-64.so.2`? ðŸ™ƒ
